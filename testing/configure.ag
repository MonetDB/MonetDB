#                                               -*- Autoconf -*-

# The contents of this file are subject to the MonetDB Public License
# Version 1.1 (the "License"); you may not use this file except in
# compliance with the License. You may obtain a copy of the License at
# http://monetdb.cwi.nl/Legal/MonetDBLicense-1.1.html
#
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
# License for the specific language governing rights and limitations
# under the License.
#
# The Original Code is the MonetDB Database System.
#
# The Initial Developer of the Original Code is CWI.
# Portions created by CWI are Copyright (C) 1997-July 2008 CWI.
# Copyright August 2008-2009 MonetDB B.V.
# All Rights Reserved.

# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.60)
AC_INIT([MonetDB Testing], [1.34.3], [monet@cwi.nl], [MonetDB-testing])
#                           ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_TOP([#include "sysdefs.h"])
AH_BOTTOM([#include "stddef.h"])
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR([src/Mtimeout.c])
AM_CONFIG_HEADER([testing_config.h:conf/config.h.in])
CONFIG_H=testing_config.h
AC_SUBST(CONFIG_H)

HOST=[$target]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

if test x"$target_os" = xcygwin; then
	AC_DEFINE(WIN32, 1, [Define on MS Windows (also under Cygwin)])
fi

# TESTING_BUILD and TESTING_SOURCE are *only* used for monetdb-testing-config
# TESTING_SOURCE is the absolute path name of the source directory
# (srcdir can be relative)

# The Q versions of various variables are used in places where \'s
# need to be escapes with an extra \.  Configure does not support \'s
# in path names, so there is no need to do anything special here
# except set the variables.  The command to set them in case we do
# need to escape the \'s is Qvar=`echo "$var" | sed 's/\\\\/\\\\\\\\/g'`
TESTING_BUILD=[`pwd`]
AC_SUBST(TESTING_BUILD)

TESTING_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(TESTING_SOURCE)

AM_MONETDB_XQ_VARS_1()


# Check for Monet 
req_monetdb_ver='1.34.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB_COMMON($req_monetdb_ver)
if test "x$have_monetdb" != xyes; then
	if test "x$MONETDB_VERSION" = "x"; then
		AC_MSG_ERROR([MonetDB was not found. Testing requires at least version $req_monetdb_ver of MonetDB.])
	else
		AC_MSG_ERROR([Testing requires at least version $req_monetdb_ver of MonetDB, but only version $MONETDB_VERSION was found.])
	fi
fi
AM_MONETDB_DEFAULTS()
AM_MONETDB_COMPILER()
AM_MONETDB_TOOLS()
AM_MONETDB_OPTIONS()
AM_MONETDB_LIBS()
AM_MONETDB_UTILS()

AM_CONDITIONAL(CROSS_COMPILING, test x$cross_compiling = xyes)

AC_PROG_LIBTOOL

# Checks for libraries.

# Checks for header files.
AC_CHECK_HEADERS([fcntl.h io.h signal.h])
AC_CHECK_HEADERS([sys/wait.h])
AM_MONETDB_LIBS()

# Checks for library functions.
AC_CHECK_FUNCS([getopt kill strdup])
AC_CHECK_DECLS([strdup])


dnl provide different versions of the paths derived above
AM_MONETDB_XQ_VARS_2()
AC_DEFINE_UNQUOTED(TESTING_PREFIX,"$QXprefix",[architecture-independent files])
AC_DEFINE_UNQUOTED(TESTING_EXEC_PREFIX,"$QXexec_prefix",[architecture-dependent files])
QTESTING_BUILD="$TESTING_BUILD"
XTESTING_BUILD="`$translatepath "$TESTING_BUILD"`"
QXTESTING_BUILD="`echo "$XTESTING_BUILD" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QTESTING_BUILD)
AC_SUBST(XTESTING_BUILD)
AC_SUBST(QXTESTING_BUILD)
QTESTING_SOURCE="$TESTING_SOURCE"
XTESTING_SOURCE="`$translatepath "$TESTING_SOURCE"`"
QXTESTING_SOURCE="`echo "$XTESTING_SOURCE" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QTESTING_SOURCE)
AC_SUBST(XTESTING_SOURCE)
AC_SUBST(QXTESTING_SOURCE)
AC_DEFINE_UNQUOTED(TESTING_LOCALSTATEDIR,"$QXlocalstatedir",[modifiable single-machine data])
AC_DEFINE_UNQUOTED(TESTING_LIBDIR,"$QXlibdir",[object code libraries])
AC_DEFINE_UNQUOTED(TESTING_SYSCONFDIR,"$QXsysconfdir",[read-only single-machine data])


case "$host_os" in
mac*)	DIRSEP=':'
	QDIRSEP=':'
	AC_ERROR([mac not supported])
	;;
*mingw*)
	DIRSEP='\'
	QDIRSEP='\\'
	PATHSEP=';'
	SOEXT='-0.dll'
	;;
*darwin*)
	DIRSEP='/'
	QDIRSEP='/'
	PATHSEP=':'
	SOEXT='.dylib'
	;;
*)
	DIRSEP='/'
	QDIRSEP='/'
	PATHSEP=':'
	SOEXT='.so'
	;;
esac
AC_SUBST(DIRSEP)
AC_SUBST(QDIRSEP)
AC_SUBST(PATHSEP)
AC_SUBST(SOEXT)

dnl  CFLAGS for our code are stricter than what autoconf can cope with.
CFLAGS="$CFLAGS \$(X_CFLAGS)"
