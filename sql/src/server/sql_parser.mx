@' The contents of this file are subject to the MonetDB Public License
@' Version 1.1 (the "License"); you may not use this file except in
@' compliance with the License. You may obtain a copy of the License at
@' http://monetdb.cwi.nl/Legal/MonetDBLicense-1.1.html
@'
@' Software distributed under the License is distributed on an "AS IS"
@' basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
@' License for the specific language governing rights and limitations
@' under the License.
@'
@' The Original Code is the MonetDB Database System.
@'
@' The Initial Developer of the Original Code is CWI.
@' Portions created by CWI are Copyright (C) 1997-2007 CWI.
@' All Rights Reserved.

@f sql_parser
@a N.J. Nes
@*

@h
#ifndef _SQL_PARSER_H_
#define _SQL_PARSER_H_

#include "sql_statement.h"
#include "sql_trans.h"

/* the next define makes the parser output more specific error messages,
 * instead of only a dull 'parse error' */
#define YYERROR_VERBOSE 1
/* enable this to get an idea of what the parser is doing on stdout
#define YYDEBUG 1
*/

typedef enum tokens {
	SQL_CREATE_SCHEMA,
	SQL_CREATE_TABLE,
	SQL_CREATE_VIEW,
	SQL_CREATE_INDEX,
	SQL_CREATE_ROLE,
	SQL_CREATE_USER,
	SQL_CREATE_TYPE,
	SQL_CREATE_FUNC,
	SQL_CREATE_SEQ,
	SQL_CREATE_TRIGGER,
	SQL_DROP_SCHEMA,
	SQL_DROP_TABLE,
	SQL_DROP_VIEW,
	SQL_DROP_INDEX,
	SQL_DROP_ROLE,
	SQL_DROP_USER,
	SQL_DROP_TYPE,
	SQL_DROP_FUNC,
	SQL_DROP_SEQ,
	SQL_DROP_TRIGGER,
	SQL_ALTER_TABLE,
	SQL_ALTER_SEQ,
	SQL_ALTER_USER,
	SQL_DROP_COLUMN,
	SQL_DROP_CONSTRAINT,
	SQL_DROP_DEFAULT,
	SQL_DECLARE,
	SQL_SET,
	SQL_PREP,
	SQL_NAME,
	SQL_USER,
	SQL_PATH,
	SQL_CHARSET,
	SQL_SCHEMA,
	SQL_TABLE,
	SQL_TYPE,
	SQL_CASE,
	SQL_CAST,
	SQL_RETURN,
	SQL_IF,
	SQL_ELSE,
	SQL_WHILE,
	SQL_COLUMN,
	SQL_COLUMN_OPTIONS,
	SQL_COALESCE,
	SQL_CONSTRAINT,
	SQL_CHECK,
	SQL_DEFAULT,
	SQL_NOT_NULL,
	SQL_NULL,
	SQL_NULLIF,
	SQL_UNIQUE,
	SQL_PRIMARY_KEY,
	SQL_FOREIGN_KEY,
	SQL_BEGIN,
	TR_COMMIT,
	TR_ROLLBACK,
	TR_SAVEPOINT,
	TR_RELEASE,
	TR_START,
	TR_MODE,
	SQL_INSERT,
	SQL_DELETE,
	SQL_UPDATE,
	SQL_CROSS,
	SQL_JOIN,
	SQL_SELECT,
	SQL_CONNECT,
	SQL_DISCONNECT,
	SQL_DATABASE,
	SQL_PORT,
	SQL_WHERE,
	SQL_FROM,
	SQL_UNIONJOIN,
	SQL_UNION,
	SQL_EXCEPT,
	SQL_INTERSECT,
	SQL_VALUES,
	SQL_ASSIGN,
	SQL_ORDERBY,
	SQL_GROUPBY,
	SQL_DESC,
	SQL_AND,
	SQL_OR,
	SQL_NOT,
	SQL_EXISTS,
	SQL_NOT_EXISTS,
	SQL_OP,
	SQL_UNOP,
	SQL_BINOP,
	SQL_NOP,
	SQL_BETWEEN,
	SQL_NOT_BETWEEN,
	SQL_LIKE,
	SQL_NOT_LIKE,
	SQL_IN,
	SQL_NOT_IN,
	SQL_GRANT,
	SQL_GRANT_ROLES,
	SQL_REVOKE,
	SQL_REVOKE_ROLES,
	SQL_EXECUTE,
	SQL_PRIVILEGES,
	SQL_ROLE,
	SQL_PARAMETER,
	SQL_FUNC,
	SQL_AGGR,
	SQL_RANK,
	SQL_COMPARE,
	SQL_TEMP_LOCAL,
	SQL_TEMP_GLOBAL,
	SQL_INT_VALUE,
	SQL_ATOM,
	SQL_USING,
	SQL_WHEN,
	SQL_ESCAPE,
	SQL_COPYFROM,
	SQL_COPYTO,
	SQL_EXPORT,
	SQL_NEXT,
	SQL_MULSTMT,
	SQL_WITH
} tokens;

typedef enum jt {
	jt_inner = 0,
	jt_left = 1,
	jt_right = 2,
	jt_full = 3,
	jt_union = 4
} jt;

extern char *token2string(int token);
extern void *sql_error(mvc *sql, int error_code, char *format, ...);
extern int parse_error(mvc *sql, char *s);
extern int sqlparse(void *);

#endif /*_SQL_PARSER_H_*/

@y
%{
#include "sql_config.h"
#include <sql_mem.h>
#include "sql_parser.h"
#include "sql_symbol.h"
#include "sql_datetime.h"
#include "sql_decimal.h"	/* for decimal_from_str() */
#include "sql_semantic.h"	/* for sql_add_param() & sql_add_arg() */
#include "sql_env.h"
#include "sql_sequence.h"	/* for sql_next_seq_name() */

#include <unistd.h>
#include <string.h>
#include <stdlib.h>

#define SA 	(((mvc*)parm)->sa)
#define _symbol_create(t,d)         symbol_create( SA, t, d)
#define _symbol_create_list(t,d)    symbol_create_list( SA, t, d)
#define _symbol_create_int(t,d)     symbol_create_int( SA, t, d)
#define _symbol_create_symbol(t,d)  symbol_create_symbol( SA, t, d)
#define _symbol_create_hexa(t,d)    symbol_create_hexa( SA, t, d)
#define _newAtomNode(d)		    newAtomNode( SA, d)

#define L()                  dlist_create( SA )

#define append_list(l,d)     dlist_append_list( SA, l, d)
#define append_int(l,d)      dlist_append_int( SA, l, d)
#define append_symbol(l,d)   dlist_append_symbol( SA, l, d)
#define append_string(l,d)   dlist_append_string( SA, l, d)
#define append_type(l,d)     dlist_append_type( SA, l, d)

#define _atom_string(t, v)   atom_string(t, v, 0)
 
#define YYPARSE_PARAM parm
#define YYLEX_PARAM parm
#ifdef yyerror
#undef yyerror
#endif
#define yyerror(s) parse_error(YYLEX_PARAM, s)

#define FALSE 0
#define TRUE 1

%}
/* KNOWN NOT DONE OF sql'99
 *
 * COLLATION
 * TRANSLATION
 * REF/SCOPE
 * UDT
 */

/* reentrant parser */
%pure_parser
%union {
	lng		ival,operation,bval;
	double		fval;
	char *		sval;
	symbol*		sym;
	dlist*		l;
	sql_subtype	type;
}
%{
extern int sqllex( YYSTYPE *yylval, void *m );
/* enable to activate debugging support
int yydebug=1;
*/
%}

	/* symbolic tokens */
%type <sym>
	user_passwd
	alter_statement
	assignment
	create_statement
	drop_statement
	declare_statement
	set_statement
	sql
	sqlstmt
	schema
	opt_schema_default_char_set
	opt_path_specification
	path_specification
	schema_element
	delete_stmt
	copyfrom_stmt
	table_def
	view_def
	query_expression
	role_def
	type_def
	func_def
	index_def
	seq_def
	all_or_any_predicate
	atom_exp
	between_predicate
	comparison_predicate
	opt_from_clause
	existence_test
	in_predicate
	insert_stmt
	transaction_statement
	_transaction_stmt
	like_predicate
	opt_where_clause
	opt_having_clause
	opt_group_by_clause
	predicate
	joined_table
	join_spec
	search_condition
	update_statement
	sql_connections_statement
	update_stmt
	connect_stmt
	disconnect_stmt
	control_statement
	select_statement_single_row
	call_statement
	routine_invocation
	return_statement
	return_value
	case_statement
	when_statement
	when_search_statement
	if_statement
	while_statement
	select_with_parens
	select_no_parens
	simple_select
	complex_select
	set_select
	subquery
	test_for_null
	values_or_query_spec
	grant
	revoke
	operation
	table_content_source
	table_element
	add_table_element
	alter_table_element
	drop_table_element
	table_constraint
	table_constraint_type
	column_def
	column_options
	column_option
	column_constraint
	column_constraint_type
	generated_column
	like_table
	domain_constraint_type
	opt_order_by_clause
	default
	default_value
	cast_value
	aggr_ref
	var_ref
	func_ref
	datetime_funcs
	string_funcs
	scalar_exp
	pred_exp
	simple_scalar_exp
	value_exp
	column_exp
	atom
	atom2
	insert_atom
	simple_atom
	literal
	null
	interval_expression
	ordering_spec
	simple_table
	table_ref
	case_exp
	cast_exp
	when_value
	when_search
	case_opt_else
	table_name
	object_name
	exec
	exec_ref
	trigger_def
	trigger_event
	opt_when
	procedure_statement
	trigger_procedure_statement
	if_opt_else
	func_data_type
	with_list_element
	window_function
	window_function_type
	window_partition_clause
	window_order_clause

%type <type>
	data_type
	datetime_type
	interval_type

%type <sval>
	opt_lang
	opt_db_alias
	opt_constraint_name
	any_all_some
	non_reserved_word
	ident
	authorization_identifier
	func_ident
	restricted_ident
	column
	authid
	grantee
	opt_alias_name
	opt_to_savepoint
	opt_using
	string
	type_alias
	varchar
	clob
	blob
	opt_begin_label
	opt_end_label
	target_specification

%type <l>
	passwd_schema
	object_privileges
	privileges
	schema_name_clause
	assignment_commalist
	opt_column_list
	column_commalist_parens
	ident_commalist
	opt_corresponding
	column_ref_commalist
	name_commalist
	schema_name_list
	column_ref
	atom_commalist
	row_commalist
	qname
	qfunc
	routine_name
	sort_specification_list
	opt_schema_element_list
	schema_element_list
	operation_commalist
	authid_list
	grantee_commalist
	column_def_opt_list
	opt_column_def_opt_list
	table_exp
	table_ref_commalist
	table_element_list
	as_subquery_clause
	column_exp_commalist
	column_option_list
	selection
	start_field
	end_field
	single_datetime_field
	interval_qualifier
	scalar_exp_list2
	when_value_list
	when_search_list
	opt_seps
	string_commalist
	paramlist
	opt_paramlist
	opt_seq_params
	opt_alt_seq_params
	serial_opt_params
	opt_restart
	triggered_action
	opt_referencing_list
	old_or_new_values_alias_list
	old_or_new_values_alias
	triggered_statement
	procedure_statement_list
	trigger_procedure_statement_list
	argument_list
	when_statements
	when_search_statements
	case_opt_else_statement
	variable_list
	routine_body
	table_function_column_list
	select_target_list
	external_function_name
	with_list
	window_specification

%type <ival>
	opt_port
	drop_action
	ref_action
	join_type
	outer_join_type
	time_precision
	timestamp_precision
	non_second_datetime_field
	datetime_field
	opt_bounds
	opt_sign
	opt_index_type
	intval
	nonzero
	posint
	opt_temp
	opt_nr
	opt_limit
	opt_offset
	opt_match
	opt_match_type
	opt_on_commit
	opt_grantor
	opt_from_grantor
	opt_column
	grantor
	transaction_mode
	transaction_mode_list
	_transaction_mode_list
	opt_start
	opt_increment
	opt_min
	opt_max
	opt_cache
	opt_cycle
	opt_ref_action
	ref_on
	with_or_without_data
	trigger_action_time
	opt_for_each
	row_or_statement

%type <bval>
	opt_brackets

	opt_work
	opt_chain
	opt_distinct
	set_distinct
	opt_with_check_option

	opt_with_grant
	opt_with_admin
	opt_admin_for
	opt_grant_for

	opt_asc_desc
	tz

%right <sval> STRING

/* sql prefixes to avoid name clashes on various architectures */
%token <sval>
	IDENT aTYPE ALIAS AGGR RANK sqlINT HEXADECIMAL INTNUM APPROXNUM USING
	ALL ANY SOME GLOBAL CAST CONVERT
	CHARACTER VARYING LARGE OBJECT VARCHAR CLOB BINARY sqlBLOB
	sqlDECIMAL sqlFLOAT
	SMALLINT BIGINT sqlINTEGER
	sqlDOUBLE sqlREAL PRECISION PARTIAL SIMPLE ACTION CASCADE RESTRICT
	BOOL_FALSE BOOL_TRUE
	CURRENT_DATE CURRENT_TIMESTAMP CURRENT_TIME LOCALTIMESTAMP LOCALTIME
	LEX_ERROR 

%token	USER CURRENT_USER SESSION_USER LOCAL
%token  CURRENT_ROLE sqlSESSION
%token <sval> sqlDELETE UPDATE SELECT INSERT DATABASE CONNECT DISCONNECT PORT CURRENT
%token <sval> LEFT RIGHT FULL OUTER NATURAL CROSS JOIN INNER
%token <sval> COMMIT ROLLBACK SAVEPOINT RELEASE WORK CHAIN NO PRESERVE ROWS
%token  START TRANSACTION READ WRITE ONLY ISOLATION LEVEL
%token  UNCOMMITTED COMMITTED REPEATABLE SERIALIZABLE DIAGNOSTICS sqlSIZE

%token <sval> ASYMMETRIC SYMMETRIC ORDER BY
%token <sval> LIKE BETWEEN
%token <operation> sqlIN EXISTS ESCAPE HAVING GROUP NULLX
%token <operation> FROM FOR MATCH

%token <operation> EXTRACT

/* sequence operations */
%token SEQUENCE INCREMENT RESTART
%token MAXVALUE MINVALUE CYCLE
%token NOMAXVALUE NOMINVALUE NOCYCLE
%token NEXT VALUE CACHE
%token GENERATED ALWAYS IDENTITY
%token SERIAL AUTO_INCREMENT /* PostgreSQL and MySQL immitators */

/* SQL's terminator, the semi-colon */
%token SCOLON AT

/* operators */
%left UNION EXCEPT INTERSECT CORRESPONDING UNIONJOIN
%left JOIN CROSS LEFT FULL RIGHT INNER NATURAL
%left LIKE BETWEEN sqlIN WITH DATA
%left <operation> OR
%left <operation> AND
%left <operation> NOT
%left <operation> '('
%left <sval> COMPARISON /* <> < > <= >= */
%left <operation> '='
%left <operation> '+' '-'
%left <operation> '*' '/' '%'
%left <operation> SUBSTRING CONCATSTRING POSITION
%right UMINUS

	/* literal keyword tokens */
/*
CONTINUE CURRENT CURSOR FOUND GOTO GO LANGUAGE
SQLCODE SQLERROR UNDER WHENEVER
*/

%token TEMPORARY
%token<sval> ASC DESC AUTHORIZATION
%token CHECK CONSTRAINT CREATE
%token TYPE PROCEDURE FUNCTION RETURNS EXTERNAL sqlNAME DECLARE
%token CALL LANGUAGE 
%token SQL_EXPLAIN SQL_PROFILE SQL_DEBUG SQL_TRACE PREPARE EXECUTE
%token DEFAULT DISTINCT DROP
%token FOREIGN
%token PASSWORD GRANT REVOKE ROLE ADMIN INTO
%token IS KEY ON OPTION OPTIONS
%token PATH PRIMARY PRIVILEGES
%token<sval> PUBLIC REFERENCES SCHEMA SET AUTO_COMMIT
%token RETURN 

%token ALTER ADD TABLE COLUMN TO UNIQUE VALUES VIEW WHERE WITH
%token<sval> sqlDATE TIME TIMESTAMP INTERVAL
%token YEAR MONTH DAY HOUR MINUTE SECOND ZONE
%token LIMIT OFFSET

%token CASE WHEN THEN ELSE NULLIF COALESCE IF ELSEIF WHILE DO
%token ATOMIC BEGIN END
%token COPY RECORDS DELIMITERS STDIN
%token INDEX

%token AS TRIGGER OF BEFORE AFTER ROW STATEMENT sqlNEW OLD EACH REFERENCING
%token OVER PARTITION 

%%

sqlstmt:
   sql SCOLON
	{
		mvc *m = (mvc*)parm;
		if (m->sym) {
			append_symbol(m->sym->data.lval, $$);
			$$ = m->sym;
		} else {
			m->sym = $$ = $1;
		}
		YYACCEPT;
	}

 | PREPARE 		{ mvc *m = (mvc*)parm;
		  	  m->mode = m_prepare; 
			  m->scanner.as = m->scanner.yycur; 
			  m->scanner.key = 0;
			}
   sql SCOLON 		{ mvc *m = (mvc*)parm;
		  	  m->sym = $$ = $3; YYACCEPT; 
			}
 | SQL_EXPLAIN 		{ mvc *m = (mvc*)parm;
		  	  m->mode = m_explain;
			  m->scanner.as = m->scanner.yycur; 
			  m->scanner.key = 0;
			}
	sql SCOLON 	{ mvc *m = (mvc*)parm;
		  	  m->sym = $$ = $3; YYACCEPT; 
			}
 | SQL_PROFILE 		{ mvc *m = (mvc*)parm;
		  	  m->mode = m_profile;
			  m->scanner.as = m->scanner.yycur; 
			  m->scanner.key = 0;
			}
	sql SCOLON 	{ mvc *m = (mvc*)parm;
		  	  m->sym = $$ = $3; YYACCEPT; 
			}
 | SQL_DEBUG 		{ mvc *m = (mvc*)parm;
		  	  m->mode = m_debug;
			  m->scanner.as = m->scanner.yycur; 
			  m->scanner.key = 0;
			}
	sql SCOLON 	{ mvc *m = (mvc*)parm;
		  	  m->sym = $$ = $3; YYACCEPT; 
			}
 | SQL_TRACE 		{ mvc *m = (mvc*)parm;
		  	  m->mode = m_trace;
			  m->scanner.as = m->scanner.yycur; 
			  m->scanner.key = 0;
			}
	sql SCOLON 	{ mvc *m = (mvc*)parm;
		  	  m->sym = $$ = $3; YYACCEPT; 
			}
 | exec SCOLON		{ mvc *m = (mvc*)parm;
			  m->sym = $$ = $1; YYACCEPT; }
 | /*empty*/		{ mvc *m = (mvc*)parm;
			  m->sym = $$ = NULL; YYACCEPT; }
 | SCOLON			{ mvc *m = (mvc*)parm;
			  m->sym = $$ = NULL; YYACCEPT; }
 | error SCOLON		{ mvc *m = (mvc*)parm;
			  m->sym = $$ = NULL; YYACCEPT; }
 | LEX_ERROR		{ mvc *m = (mvc*)parm;
			  m->sym = $$ = NULL; YYACCEPT; }
 ;


create:
    CREATE 		{ mvc *m = (mvc*)parm;
			  if (m->mode == m_normal)
		  	  	m->mode = m_inplace; }
set:
    SET 		{ mvc *m = (mvc*)parm;
			  if (m->mode == m_normal)
		  	  	m->mode = m_inplace; }

declare:
    DECLARE 		{ mvc *m = (mvc*)parm;
			  if (m->mode == m_normal)
		  	  	m->mode = m_inplace; }

	/* schema definition language */
sql:
    schema
 |  grant
 |  revoke
 |  create_statement
 |  drop_statement
 |  alter_statement
 |  declare_statement
 |  set_statement
 ;

declare_statement:
	declare variable_list
		{ $$ = _symbol_create_list( SQL_DECLARE, $2); }
    |   declare table_def { $$ = $2; }
    ;

variable_list:
	ident_commalist data_type
		{ dlist *l = L();
		append_list(l, $1 );
		append_type(l, &$2 );
		$$ = append_symbol(L(), _symbol_create_list( SQL_DECLARE, l)); }
    |	variable_list ',' ident_commalist data_type
		{ dlist *l = L();
		append_list(l, $3 );
		append_type(l, &$4 );
		$$ = append_symbol($1, _symbol_create_list( SQL_DECLARE, l)); }
    ;

set_statement:
	set ident '=' scalar_exp
		{ dlist *l = L();
		append_string(l, $2 );
		append_symbol(l, $4 );
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set sqlSESSION AUTHORIZATION ident
		{ dlist *l = L();
		  sql_subtype t;
	        sql_find_subtype(&t, "char", _strlen($4), 0 );
		append_string(l, sa_strdup(SA, "current_user"));
		append_symbol(l,
			_newAtomNode( _atom_string(&t, sql2str($4))) );
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set SCHEMA ident
		{ dlist *l = L();
		  sql_subtype t;
		sql_find_subtype(&t, "char", _strlen($3), 0 );
		append_string(l, sa_strdup(SA, "current_schema"));
		append_symbol(l,
			_newAtomNode( _atom_string(&t, sql2str($3))) );
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set user '=' ident
		{ dlist *l = L();
		  sql_subtype t;
		sql_find_subtype(&t, "char", _strlen($4), 0 );
		append_string(l, sa_strdup(SA, "current_user"));
		append_symbol(l,
			_newAtomNode( _atom_string(&t, sql2str($4))) );
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set ROLE ident
		{ dlist *l = L();
		  sql_subtype t;
		sql_find_subtype(&t, "char", _strlen($3), 0);
		append_string(l, sa_strdup(SA, "current_role"));
		append_symbol(l,
			_newAtomNode( _atom_string(&t, sql2str($3))) );
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set TIME ZONE LOCAL
		{ dlist *l = L();
		append_string(l, sa_strdup(SA, "current_timezone"));
		append_symbol(l, _symbol_create_list( SQL_OP, append_list(L(),
			append_string( L(), sa_strdup(SA, "local_timezone")))));
		$$ = _symbol_create_list( SQL_SET, l); }
  |	set TIME ZONE interval_expression
		{ dlist *l = L();
		append_string(l, sa_strdup(SA, "current_timezone"));
		append_symbol(l, $4 );
		$$ = _symbol_create_list( SQL_SET, l); }
  ;

schema:
	create SCHEMA schema_name_clause opt_schema_default_char_set
			opt_path_specification	opt_schema_element_list
		{ dlist *l = L();
		append_list(l, $3);
		append_symbol(l, $4);
		append_symbol(l, $5);
		append_list(l, $6);
		$$ = _symbol_create_list( SQL_CREATE_SCHEMA, l); }
  |	DROP SCHEMA qname drop_action
		{ dlist *l = L();
		append_list(l, $3);
		append_int(l, $4);
		$$ = _symbol_create_list( SQL_DROP_SCHEMA, l); }
 ;

schema_name_clause:
    ident
	{ $$ = L();
	  append_string($$, $1 );
	  append_string($$, NULL ); }
 |  AUTHORIZATION authorization_identifier
	{ $$ = L();
	  append_string($$, NULL );
	  append_string($$, $2 ); }
 |  ident AUTHORIZATION authorization_identifier
	{ $$ = L();
	  append_string($$, $1 );
	  append_string($$, $3 ); }
 ;

authorization_identifier:
	ident	/* role name | user identifier */ ;

opt_schema_default_char_set:
    /* empty */			{ $$ = NULL; }
 |  DEFAULT CHARACTER SET ident { $$ = _symbol_create( SQL_CHARSET, $4 ); }
 ;

opt_schema_element_list:
    /* empty */			{ $$ = L(); }
 |  schema_element_list
 ;

schema_element_list:
    schema_element	{ $$ = append_symbol(L(), $1); }
 |  schema_element_list schema_element
			{ $$ = append_symbol( $1, $2 ); }
 ;

schema_element: grant | revoke | create_statement | drop_statement | alter_statement ;

opt_grantor:
     /* empty */	 { $$ = cur_user; }
 |   WITH ADMIN grantor  { $$ = $3; }
 ;

grantor:
    CURRENT_USER	{ $$ = cur_user; }
 |  CURRENT_ROLE	{ $$ = cur_role; }
 ;

grant:
    GRANT privileges TO grantee_commalist opt_with_grant opt_from_grantor
	{ dlist *l = L();
	  append_list(l, $2);
	  append_list(l, $4);
	  append_int(l, $5);
	  append_int(l, $6);
	$$ = _symbol_create_list( SQL_GRANT, l);
	}

 |  GRANT authid_list TO grantee_commalist opt_with_admin
		opt_from_grantor
	{ dlist *l = L();
	  append_list(l, $2);
	  append_list(l, $4);
	  append_int(l, $5);
	  append_int(l, $6);
	$$ = _symbol_create_list( SQL_GRANT_ROLES, l); }
 ;

authid_list:
	authid		{ $$ = append_string(L(), $1); }
 |	authid_list ',' authid
			{ $$ = append_string($1, $3); }
 ;

opt_with_grant:
    /* empty */				{ $$ = 0; }
 |	WITH GRANT OPTION		{ $$ = 1; }
 ;

opt_with_admin:
	/* emtpy */		{ $$ = 0; }
 |	WITH ADMIN OPTION	{ $$ = 1; }
 ;


opt_from_grantor:
	/* empty */	{ $$ = cur_user; }
 |	FROM grantor	{ $$ = $2; }
 ;

revoke:
     REVOKE opt_grant_for privileges FROM grantee_commalist opt_from_grantor
	{ dlist *l = L();
	  append_list(l, $3);
	  append_list(l, $5);
	  append_int(l, $2); /* GRANT OPTION FOR */
	  append_int(l, 0);
	  append_int(l, $6);
	$$ = _symbol_create_list( SQL_REVOKE, l); }
 |   REVOKE opt_admin_for authid_list FROM grantee_commalist opt_from_grantor
	{ dlist *l = L();
	  append_list(l, $3);
	  append_list(l, $5);
	  append_int(l, $6);
	  append_int(l, $2);
	$$ = _symbol_create_list( SQL_REVOKE, l); }
 ;

opt_grant_for:
	/* empty */			{ $$ = 0; }
 |	GRANT OPTION FOR		{ $$ = 1; }
 ;

opt_admin_for:
	/* empty */			{ $$ = 0; }
 |	ADMIN OPTION FOR		{ $$ = 1; }
 ;

privileges:
	object_privileges ON object_name
	{ $$ = L();
	  append_list($$, $1);
	  append_symbol($$, $3); }
 ;

object_name:
     TABLE ident		{ $$ = _symbol_create(SQL_TABLE, $2); }
 |   ident			{ $$ = _symbol_create(SQL_NAME, $1); }

/* | DOMAIN domain_name
   | CHARACTER SET char_set_name
   | COLLATION collation_name
   | TRANSLATION trans_name
   | TYPE udt_name
   | TYPE typed_table_name
*/
 ;

object_privileges:
    ALL PRIVILEGES			{ $$ = NULL; }
 |  ALL					{ $$ = NULL; }
 |  operation_commalist
 ;

operation_commalist:
    operation		{ $$ = append_symbol(L(), $1); }
 |  operation_commalist ',' operation
			{ $$ = append_symbol($1, $3); }
 ;

operation:
    INSERT			    { $$ = _symbol_create(SQL_INSERT,NULL); }
 |  sqlDELETE			    { $$ = _symbol_create(SQL_DELETE,NULL); }
 |  UPDATE opt_column_list          { $$ = _symbol_create_list(SQL_UPDATE,$2); }
 |  SELECT opt_column_list	    { $$ = _symbol_create_list(SQL_SELECT,$2); }
 |  REFERENCES opt_column_list 	    { $$ = _symbol_create_list(SQL_SELECT,$2); }
 |  EXECUTE			    { $$ = _symbol_create(SQL_EXECUTE,NULL); }
 ;

grantee_commalist:
    grantee			{ $$ = append_string(L(), $1); }
 |  grantee_commalist ',' grantee
				{ $$ = append_string($1, $3); }
 ;

grantee:
    PUBLIC			{ $$ = NULL; }
 |  authid			{ $$ = $1; }
 ;

/* DOMAIN, ASSERTION, CHARACTER SET, TRANSLATION, TRIGGER */

alter_statement:
   ALTER TABLE qname ADD opt_column add_table_element

	{ dlist *l = L();
	  append_list(l, $3);
	  append_symbol(l, $6);
	  $$ = _symbol_create_list( SQL_ALTER_TABLE, l ); }
 | ALTER TABLE qname ALTER alter_table_element

	{ dlist *l = L();
	  append_list(l, $3);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_ALTER_TABLE, l ); }
 | ALTER TABLE qname DROP drop_table_element
	{ dlist *l = L();
	  append_list(l, $3);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_ALTER_TABLE, l ); }
 | ALTER USER ident passwd_schema
	{ dlist *l = L();
	  append_string(l, $3);
	  append_list(l, $4);
	  $$ = _symbol_create_list( SQL_ALTER_USER, l ); }
  ;

passwd_schema:
  	WITH PASSWORD string	{ dlist * l = L();
				  append_string(l, $3);
				  append_string(l, NULL);
				  $$ = l; }
  |	SET SCHEMA ident	{ dlist * l = L();
				  append_string(l, NULL);
				  append_string(l, $3);
				  $$ = l; }
  |	WITH PASSWORD string SET SCHEMA ident	
				{ dlist * l = L();
				  append_string(l, $3);
				  append_string(l, $6);
				  $$ = l; }
  ;

alter_table_element:
	opt_column ident SET DEFAULT default_value
	{ dlist *l = L();
	  append_string(l, $2);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_DEFAULT, l); }
 |	opt_column ident DROP DEFAULT
	{ $$ = _symbol_create( SQL_DROP_DEFAULT, $2); }
 ;

drop_table_element:
     opt_column ident drop_action
	{ dlist *l = L();
	  append_string(l, $2 );
	  append_int(l, $3 );
	  $$ = _symbol_create_list( SQL_DROP_COLUMN, l ); }
  |  CONSTRAINT ident drop_action
	{ dlist *l = L();
	  append_string(l, $2 );
	  append_int(l, $3 );
	  $$ = _symbol_create_list( SQL_DROP_CONSTRAINT, l ); }
  ;

opt_column:
     COLUMN	 { $$ = 0; }
 |   /* empty */ { $$ = 0; }
 ;

create_statement:	
   role_def 
 | create table_def 	{ $$ = $2; }
 | view_def
 | type_def
 | func_def
 | index_def
 | seq_def
 | trigger_def
 ;

/*=== BEGIN SEQUENCES ===*/
seq_def:
/*
 * CREATE SEQUENCE name AS datatype
 * 	[ START WITH start ] 
 * 	[ INCREMENT BY increment ]
 * 	[ MINVALUE minvalue | NO MINVALUE ]
 * 	[ MAXVALUE maxvalue | NO MAXVALUE ]
 * 	[ CACHE cache ]
 * 	[ [ NO ] CYCLE ]
 * start may be a value or subquery
 */
    create SEQUENCE qname
    AS data_type
    opt_seq_params
	{
		dlist *l = L();
		append_list(l, $3);
		append_type(l, &$5);
		append_list(l, $6);
		$$ = _symbol_create_list(SQL_CREATE_SEQ, l);
	}
/*
 * DROP SEQUENCE name
 */
  | DROP SEQUENCE qname
	{
		dlist *l = L();
		append_list(l, $3);
		$$ = _symbol_create_list(SQL_DROP_SEQ, l);
	}
/*
 * ALTER SEQUENCE name
 * 	[ RESTART [ WITH start ] ] 
 * 	[ INCREMENT BY increment ]
 * 	[ MINVALUE minvalue | NO MINVALUE ]
 * 	[ MAXVALUE maxvalue | NO MAXVALUE ]
 * 	[ CACHE cache ]
 * 	[ [ NO ] CYCLE ]
 * start may be a value or subquery
 */
  | ALTER SEQUENCE qname opt_alt_seq_params
	{
		dlist *l = L();
		append_list(l, $3);
		append_type(l, NULL);
		append_list(l, $4);
		$$ = _symbol_create_list(SQL_ALTER_SEQ, l);
	}
 ;

opt_seq_params:
	opt_start
	opt_increment
	opt_min
	opt_max
	opt_cache
	opt_cycle
	{ dlist *l = L();
	  append_int(l, $1);
	  append_int(l, $2);
	  append_int(l, $3);
	  append_int(l, $4);
	  append_int(l, $5);
	  append_int(l, $6);
	  $$ = l;
	}
;

opt_alt_seq_params:
	opt_restart
	opt_increment
	opt_min
	opt_max
	opt_cache
	opt_cycle
	{ dlist *l = L();
	  append_list(l, $1);
	  append_int(l, $2);
	  append_int(l, $3);
	  append_int(l, $4);
	  append_int(l, $5);
	  append_int(l, $6);
	  $$ = l;
	}
;

/*
 * opt_start returns sequence start number 
 */
opt_start:
   	/* empty */		{ $$ = 1; }
  |	START WITH nonzero 	{ $$ = $3; }
  ;

/*
 * opt_restart returns a list consisting of:
 * - int: indicating the type of symbol
 *   * -1  absent/empty
 *   *  0  no argument (only for RESTART)
 *   *  1  argument is a subquery
 *   *  2  argument is simple int
 * - symbol: the symbol
 */
opt_restart:
	/* empty */				{
								dlist *l = L();
								append_int(l, -1);
								$$ = l;
							}
  |	RESTART					{
								dlist *l = L();
								append_int(l, 0);
								$$ = l;
							}
  | RESTART WITH			{
								mvc *m = (mvc*)parm;
								m->scanner.as = m->scanner.yycur;
							}
				subquery	{
								mvc *m = (mvc*)parm;
								dlist *l = L();
								append_int(l, 1);
								append_symbol(l, $4);
								$$ = l;
								m->scanner.view_end = m->scanner.yycur;
							}
  |	RESTART WITH nonzero	{
								dlist *l = L();
								append_int(l, 2);
								append_int(l, $3);
								$$ = l;
							}
  ;

/*
 * opt_increment returns a list consisting of:
 * - int: indicating the value
 *   * -1  absent/empty
 *   * else the value
 */
opt_increment:
	/* empty */				{ $$ = -1; }
  |	INCREMENT BY nonzero	{ $$ = $3; }
  ;

/*
 * opt_min returns a list consisting of:
 * - int: indicating the value
 *   * -1  absent/empty
 *   *  0  NOMINVALUE
 *   * else the MINVALUE value
 */
opt_min:
	/* empty */ 			{ $$ = -1; }
  |	MINVALUE nonzero		{ $$ = $2; }
  |	NOMINVALUE				{ $$ =  0; }
  ;

/*
 * opt_max returns a list consisting of:
 * - int: indicating the value
 *   * -1  absent/empty
 *   *  0  NOMAXVALUE
 *   * else the MAXVALUE value
 */
opt_max:
	/* empty */				{ $$ = -1; }
  |	MAXVALUE nonzero		{ $$ = $2; }
  |	NOMAXVALUE				{ $$ =  0; }
  ;

/*
 * opt_cache returns a list consisting of:
 * - int: indicating the value
 *   * -1  absent/empty
 *   * else the value
 */
opt_cache:
	/* empty */				{ $$ = -1; }
  |	CACHE nonzero			{ $$ = $2; }
  ;

/*
 * opt_cycle returns a list consisting of:
 * - int: indicating the value
 *   *  0  NOCYCLE (default)
 *   *  1  CYCLE
 */
opt_cycle:
	/* empty */				{ $$ = 0; }
  |	CYCLE					{ $$ = 1; }
  |	NOCYCLE					{ $$ = 0; }
  ;
/*=== END SEQUENCES ===*/


index_def:
    create opt_index_type INDEX ident ON qname '(' ident_commalist ')'
	{ dlist *l = L();
	  append_string(l, $4);
	  append_int(l, $2);
	  append_list(l, $6);
	  append_list(l, $8);
	  $$ = _symbol_create_list( SQL_CREATE_INDEX, l); }
  ;

opt_index_type:
     UNIQUE		{ $$ = unique; }
 |   /* empty */	{ $$ = unique; }
 ;

/* sql-server def
CREATE [ UNIQUE ] [ CLUSTERED | NONCLUSTERED ] INDEX index_name
    ON { table | view } ( column [ ASC | DESC ] [ ,...n ] )
[ WITH < index_option > [ ,...n] ]
[ ON filegroup ]

< index_option > :: =
    { PAD_INDEX |
        FILLFACTOR = fillfactor |
        IGNORE_DUP_KEY |
        DROP_EXISTING |
    STATISTICS_NORECOMPUTE |
    SORT_IN_TEMPDB
}
*/

role_def:
    create ROLE qname opt_grantor
	{ dlist *l = L();
	  append_list(l, $3);
	  append_int(l, $4);
	  $$ = _symbol_create_list( SQL_CREATE_ROLE, l ); }
 |  create USER ident WITH PASSWORD string sqlNAME string SCHEMA ident
	{ dlist *l = L();
	  append_string(l, $3);
	  append_string(l, $6);
	  append_string(l, $8);
	  append_string(l, $10);
	  $$ = _symbol_create_list( SQL_CREATE_USER, l ); }
 ;

table_def:
    opt_temp TABLE qname table_content_source opt_on_commit 
	{ int commit_action = CA_COMMIT;
	  dlist *l = L();
	  append_int(l, $1);
	  append_list(l, $3);
	  append_symbol(l, $4);
	  if ($1 != SQL_PERSIST)
		commit_action = $5;
	  append_int(l, commit_action);
	  $$ = _symbol_create_list( SQL_CREATE_TABLE, l ); }
 ;

opt_temp:
    /* empty */		{ $$ = SQL_PERSIST; }
 |  TEMPORARY		{ $$ = SQL_LOCAL_TEMP; }
 |  LOCAL TEMPORARY	{ $$ = SQL_LOCAL_TEMP; }
 |  GLOBAL TEMPORARY	{ $$ = SQL_GLOBAL_TEMP; }
 ;

opt_on_commit: /* only for temporary tables */
    /* empty */			 { $$ = CA_DELETE; } 
 |  ON COMMIT sqlDELETE ROWS	 { $$ = CA_DELETE; }
 |  ON COMMIT PRESERVE ROWS	 { $$ = CA_PRESERVE; }
 |  ON COMMIT DROP	 	 { $$ = CA_DROP; }
 ;

table_content_source:
    '(' table_element_list ')'	{ $$ = _symbol_create_list( SQL_CREATE_TABLE, $2); }
 |  as_subquery_clause		{ $$ = _symbol_create_list( SQL_SELECT, $1); }		
 ;

as_subquery_clause:
	opt_column_list
	AS
	query_expression			
	with_or_without_data
			{ $$ = append_list(L(), $1);
			  append_symbol($$, $3); 
			  append_int($$, $4); }
 ;

with_or_without_data:
     WITH NO DATA  	{ $$ = 0; }
 |   WITH DATA 		{ $$ = 1; }
 ;

table_element_list:
    table_element
			{ $$ = append_symbol(L(), $1); }
 |  table_element_list ',' table_element
			{ $$ = append_symbol( $1, $3 ); }
 ;

add_table_element: column_def | table_constraint ;
table_element: add_table_element | column_options | like_table ;

column_def:
	column data_type opt_column_def_opt_list
		{
			dlist *l = L();
			append_string(l, $1);
			append_type(l, &$2);
			append_list(l, $3);
			$$ = _symbol_create_list(SQL_COLUMN, l);
		}
 |  column SERIAL
		{ /* SERIAL = INTEGER GENERATED ALWAYS AS IDENTITY PRIMARY KEY */
			/* handle multi-statements by wrapping them in a list */
			sql_subtype it;
			dlist* stmts;
			mvc *m = (mvc*)parm;
			/* note: sql_next_seq_name uses sa_alloc */
			str sn = sql_next_seq_name(m);
			dlist *p; /* primary key */
			/* sequence generation code */
			dlist *l = L();
			/* finally all the options */
			dlist *o = L();
			/* the name of the sequence */
			append_list(l, append_string(L(), sn));
			/* ultra dirty: inline 'integer' type generation */
			sql_find_subtype(&it, "int", 32, 0);
			append_type(l, &it);
			/* finally all the options (no defaults here) */
			append_int(o, 1); /* start */
			append_int(o, 1); /* increment */
			append_int(o, 1); /* min */
			append_int(o, 0); /* max */
			append_int(o, 1); /* cache */
			append_int(o, 0); /* cycle */
			append_list(l, o);

			if (m->sym) {
				stmts = m->sym->data.lval;
			} else {
				stmts = L();
				m->sym = _symbol_create_list(SQL_MULSTMT, stmts);
			}	
			append_symbol(stmts, _symbol_create_list(SQL_CREATE_SEQ, l));

			l = L();
			append_string(l, $1);
			append_type(l, &it);
			o = L();
			append_symbol(o, _symbol_create_symbol(SQL_DEFAULT, _symbol_create(SQL_NEXT, sn)));
			p = L();
			append_string(p, NULL);
			append_symbol(p, _symbol_create(SQL_PRIMARY_KEY, NULL));
			append_symbol(o, _symbol_create_list(SQL_CONSTRAINT, p));
			append_list(l, o);
			$$ = _symbol_create_list(SQL_COLUMN, l);
		}
 ;

opt_column_def_opt_list:
    /* empty */			{ $$ = NULL; }
 | column_def_opt_list
 ;

column_def_opt_list:
    column_option
			{ $$ = append_symbol(L(), $1 ); }
 |  column_def_opt_list column_option
			{ $$ = append_symbol( $1, $2 ); }
 ;

column_options:
    ident WITH OPTIONS '(' column_option_list ')'

	{ dlist *l = L();
	  append_string(l, $1 );
	  append_list(l, $5 );
	  $$ = _symbol_create_list( SQL_COLUMN_OPTIONS, l ); }
 ;

column_option_list:
    column_option
			{ $$ = append_symbol(L(), $1 ); }
 |  column_option_list ',' column_option
			{ $$ = append_symbol($1, $3 ); }
 ;

column_option: default | column_constraint | generated_column;

default:
    DEFAULT default_value { $$ = _symbol_create_symbol(SQL_DEFAULT, $2); }
 ;

default_value:
    simple_scalar_exp 	{ $$ = $1; }
 |  NULLX 	{ $$ = _newAtomNode( NULL);  }
 ;

column_constraint:
    opt_constraint_name column_constraint_type  /*opt_constraint_attributes*/

	{ dlist *l = L();
	  append_string(l, $1 );
	  append_symbol(l, $2 );
	  $$ = _symbol_create_list( SQL_CONSTRAINT, l ); }
 ;

generated_column:
	GENERATED ALWAYS AS IDENTITY serial_opt_params
	{
		/* handle multi-statements by wrapping them in a list */
		sql_subtype it;
		dlist* stmts;
		mvc *m = (mvc*)parm;
		/* note: sql_next_seq_name uses sa_alloc */
		str sn = sql_next_seq_name(m);
		/* sequence generation code */
		dlist *l = L();
		/* the name of the sequence */
		append_list(l, append_string(L(), sn));
		/* ultra dirty: inline 'integer' type generation */
		sql_find_subtype(&it, "int", 32, 0);
		append_type(l, &it);

		/* finally all the options */
		append_list(l, $5);
		$$ = _symbol_create_symbol(SQL_DEFAULT, _symbol_create(SQL_NEXT, sn));

		if (m->sym) {
			stmts = m->sym->data.lval;
		} else {
			stmts = L();
			m->sym = _symbol_create_list(SQL_MULSTMT, stmts);
		}	
		append_symbol(stmts, _symbol_create_list(SQL_CREATE_SEQ, l));
	}
 |	AUTO_INCREMENT
	{
		/* handle multi-statements by wrapping them in a list */
		sql_subtype it;
		dlist* stmts;
		mvc *m = (mvc*)parm;
		/* note: sql_next_seq_name uses sa_alloc */
		str sn = sql_next_seq_name(m);
		/* sequence generation code */
		dlist *l = L();
		/* finally all the options */
		dlist *o = L();
		/* the name of the sequence (not known here) */
		append_list(l, append_string(L(), sn));
		/* ultra dirty: inline 'integer' type generation */
		sql_find_subtype(&it, "int", 32, 0);
		append_type(l, &it);
		/* finally all the options (no defaults here) */
		append_int(o, 1); /* start */
		append_int(o, 1); /* increment */
		append_int(o, 1); /* min */
		append_int(o, 0); /* max */
		append_int(o, 1); /* cache */
		append_int(o, 0); /* cycle */
		append_list(l, o);
		$$ = _symbol_create_symbol(SQL_DEFAULT, _symbol_create(SQL_NEXT, sn));

		if (m->sym) {
			stmts = m->sym->data.lval;
		} else {
			stmts = L();
			m->sym = _symbol_create_list(SQL_MULSTMT, stmts);
		}	
		append_symbol(stmts, _symbol_create_list(SQL_CREATE_SEQ, l));
	}
 ;

serial_opt_params:
	/* empty: return the defaults */
	{ $$ = L();
	  /* finally all the options (no defaults here) */
	  append_int($$, 1); /* start */
	  append_int($$, -1); /* increment */
	  append_int($$, -1); /* min */
	  append_int($$, -1); /* max */
	  append_int($$, -1); /* cache */
	  append_int($$, 0);  /* cycle */
	}
  |	'(' opt_seq_params ')'	{ $$ = $2; }
 ;


table_constraint:
    opt_constraint_name table_constraint_type  /*opt_constraint_attributes*/

	{ dlist *l = L();
	  append_string(l, $1 );
	  append_symbol(l, $2 );
	  $$ = _symbol_create_list( SQL_CONSTRAINT, l ); }
 ;

/* opt_constraint_attributes: ; */

opt_constraint_name:
    /* empty */			{ $$ = NULL; }
 |  CONSTRAINT ident		{ $$ = $2; }
 ;

ref_action:
	NO ACTION		{ $$ = 0; }
 |	CASCADE			{ $$ = 1; }
 |	RESTRICT		{ $$ = 2; }
 |	SET NULLX		{ $$ = 3; }
 |	SET DEFAULT		{ $$ = 4; }
 ;

ref_on:
   ON UPDATE ref_action		{ $$ = ($3 << 8); }
 | ON sqlDELETE ref_action	{ $$ = $3; }
 ;

opt_ref_action:
   /* empty */		{ $$ = (2 << 8) + 2; /* defaults are RESTRICT */ }
 | ref_on		{ $$ = $1; }
 | ref_on ref_on	{ $$ = $1 + $2;	}
 ;

opt_match_type:
    /* empty */			{ $$ = 0; }
 | FULL				{ $$ = 1; }
 | PARTIAL			{ $$ = 2; }
 | SIMPLE			{ $$ = 0; }
 ;

opt_match:
    /* empty */			{ $$ = 0; }
 | MATCH opt_match_type		{ $$ = $2; }
 ;

column_constraint_type:
    NOT NULLX	{ $$ = _symbol_create( SQL_NOT_NULL, NULL); }
 |  NULLX	{ $$ = _symbol_create( SQL_NULL, NULL); }
 |  UNIQUE	{ $$ = _symbol_create( SQL_UNIQUE, NULL ); }
 |  PRIMARY KEY	{ $$ = _symbol_create( SQL_PRIMARY_KEY, NULL ); }
 |  REFERENCES qname '(' column_ref ')' opt_match opt_ref_action

			{ dlist *l = L();
			  append_list(l, $2 );
			  append_list(l, $4 );
			  append_int(l, $6 );
			  append_int(l, $7 );
			  $$ = _symbol_create_list( SQL_FOREIGN_KEY, l); }
 | domain_constraint_type
 ;

table_constraint_type:
    UNIQUE column_commalist_parens
			{ $$ = _symbol_create_list( SQL_UNIQUE, $2); }
 |  PRIMARY KEY column_commalist_parens
			{ $$ = _symbol_create_list( SQL_PRIMARY_KEY, $3); }
 |  FOREIGN KEY column_commalist_parens
    REFERENCES qname opt_column_list opt_match opt_ref_action

			{ dlist *l = L();
			  append_list(l, $5 );
			  append_list(l, $3 );
			  append_list(l, $6 );
			  append_int(l, $7 );
			  append_int(l, $8 );
			  $$ = _symbol_create_list( SQL_FOREIGN_KEY, l); }

 |  domain_constraint_type
 ;

domain_constraint_type:
    CHECK '(' search_condition ')' { $$ = _symbol_create_symbol(SQL_CHECK, $3); }
 ;

ident_commalist:
    ident
			{ $$ = append_string(L(), $1); }
 |  ident_commalist ',' ident
			{ $$ = append_string( $1, $3 ); }
 ;

like_table:
	LIKE qname	{ $$ = _symbol_create_list(SQL_LIKE, $2 ); }
 ;

view_def:
    create VIEW qname opt_column_list AS query_expression opt_with_check_option
	{  dlist *l = L();
	  append_list(l, $3);
	  append_list(l, $4);
	  append_symbol(l, $6);
	  append_int(l, $7);
	  append_int(l, TRUE);	/* persistent view */
	  $$ = _symbol_create_list( SQL_CREATE_VIEW, l ); 
	}
  ;

query_expression:
	select_no_parens
  ;

opt_with_check_option:
    /* empty */			{ $$ = FALSE; }
 |  WITH CHECK OPTION		{ $$ = TRUE; }
 ;

opt_column_list:
    /* empty */			{ $$ = NULL; }
 | column_commalist_parens
 ;

column_commalist_parens:
   '(' ident_commalist ')'	{ $$ = $2; }
 ;

type_def:
    create TYPE qname EXTERNAL sqlNAME ident
			{ dlist *l = L();
				append_list(l, $3);
				append_string(l, $6);
			  $$ = _symbol_create_list( SQL_CREATE_TYPE, l ); }
 ;

external_function_name:
	ident '.' ident { $$ = append_string(append_string(L(), $1), $3); }
 ;

func_def:
    create FUNCTION qname
	'(' opt_paramlist ')'
    RETURNS func_data_type
    EXTERNAL sqlNAME external_function_name 	
			{ dlist *f = L();
				append_list(f, $3);
				append_list(f, $5);
				append_symbol(f, $8);
				append_list(f, $11);
				append_list(f, NULL);
			  $$ = _symbol_create_list( SQL_CREATE_FUNC, f ); }
 |
    create FUNCTION qname
	'(' opt_paramlist ')'
    RETURNS func_data_type
    routine_body
			{ dlist *f = L();
				append_list(f, $3);
				append_list(f, $5);
				append_symbol(f, $8);
				append_string(f, NULL);
				append_list(f, $9);
			  $$ = _symbol_create_list( SQL_CREATE_FUNC, f ); }
 | /* proc ie no result */
    create PROCEDURE qname
	'(' opt_paramlist ')'
    routine_body
			{ dlist *f = L();
				append_list(f, $3);
				append_list(f, $5);
				append_symbol(f, NULL);   /* no result */
				append_string(f, NULL); /* no mil-impl */
				append_list(f, $7);
			  $$ = _symbol_create_list( SQL_CREATE_FUNC, f ); }
 ;

routine_body:
	procedure_statement 
		{ $$ = L(); append_symbol( $$, $1); }
 |  BEGIN
	procedure_statement_list procedure_statement SCOLON 
    END
		{ $$ = append_symbol($2,$3); }
 |  BEGIN ATOMIC
	procedure_statement_list procedure_statement SCOLON 
    END
		{ $$ = append_symbol($3,$4); }
 ;

/* change into compound statement 
<compound statement> ::=
                [ <beginning label> <colon> ] BEGIN [ [ NOT ] ATOMIC ]
                [ <local declaration list> ] [ <local cursor declaration list> ] [ <local handler declaration list> ]
                [ <SQL statement list> ] END [ <ending label> ]

<beginning label> ::= <statement label>

<statement label> ::= <identifier>
*/

procedure_statement_list:
    /* empty*/ 	 { $$ = L(); }
 |  procedure_statement_list 
    procedure_statement SCOLON 	{ $$ = append_symbol($1,$2);}
 ;

trigger_procedure_statement_list:
    /* empty*/ 	 { $$ = L(); }
 |  trigger_procedure_statement_list 
    trigger_procedure_statement SCOLON 	{ $$ = append_symbol($1,$2);}
 ;

procedure_statement:
	transaction_statement
    |	update_statement
    |	schema
    | 	grant
    | 	revoke
    | 	create_statement
    |	drop_statement
    |	alter_statement
    |   declare_statement
    |   set_statement
    |	control_statement
    |   select_statement_single_row
    ;

trigger_procedure_statement:
	transaction_statement
    |	update_statement
    | 	grant
    | 	revoke
    |   declare_statement
    |   set_statement
    |	control_statement
    |   select_statement_single_row
    ;

control_statement:
	call_statement
    |   while_statement
    |   if_statement
    |   case_statement
    |	return_statement
/*
    |   for_statement		fetch tuples, not supported because of cursors 

    |   loop_statement		while (true) 
    |   repeat_statement	do while 

    |   leave_statement 	multilevel break 
    |   iterate_statement	multilevel continue 
*/
    ;

call_statement:
	CALL routine_invocation 	{ $$ = $2; }
    ;

routine_invocation: 
	routine_name '(' argument_list ')'
		{ dlist *l = L(); 
		  append_list( l, $1);
		  append_list( l, $3);
		  $$ = _symbol_create_list( SQL_FUNC, l);
		}
    ;

routine_name: qname ;

argument_list:
    scalar_exp 		{ $$ = append_symbol( L(), $1); }
 |  argument_list ',' scalar_exp
			{ $$ = append_symbol( $1, $3); }
 ;


return_statement:
        RETURN return_value { $$ = _symbol_create_symbol(SQL_RETURN, $2); }
   ;

return_value:
      simple_scalar_exp
   |  complex_select
   |  search_condition
   |  TABLE '(' complex_select ')'	
		{ $$ = _symbol_create_symbol(SQL_TABLE, $3); }
   |  NULLX 	{ $$ = _newAtomNode( NULL);  }
   ;

case_statement:
     CASE scalar_exp when_statements case_opt_else_statement END CASE
		{ $$ = _symbol_create_list(SQL_CASE,
		   append_list(
		    append_list(
		     append_symbol(
		      L(),$2),$3),$4)); }
 |   CASE when_search_statements case_opt_else_statement END CASE
		 { $$ = _symbol_create_list(SQL_CASE,
		   append_list(
		    append_list(
		     L(),$2),$3)); }
 ;

when_statement:
    WHEN scalar_exp THEN procedure_statement_list
			{ $$ = _symbol_create_list( SQL_WHEN,
			   append_list(
			    append_symbol(
			     L(), $2),$4)); }
 ;

when_statements:
    when_statement
			{ $$ = append_symbol( L(), $1);}
 |  when_statements when_statement
			{ $$ = append_symbol( $1, $2); }
 ;

when_search_statement:
    WHEN search_condition THEN procedure_statement_list
			{ $$ = _symbol_create_list( SQL_WHEN,
			   append_list(
			    append_symbol(
			     L(), $2),$4)); }
 ;

when_search_statements:
    when_search_statement
			{ $$ = append_symbol( L(), $1); }
 |  when_search_statements when_search_statement
			{ $$ = append_symbol( $1, $2); }
 ;

case_opt_else_statement:
    /* empty */	        		{ $$ = NULL; }
 |  ELSE procedure_statement_list	{ $$ = $2; }
 ;

		/* data types, more types to come */


if_statement:
	IF search_condition THEN procedure_statement_list 
	if_opt_else
	END IF
		{ dlist *l = L();
		  append_symbol(l, $2);
		  append_list(l, $4);
		  append_symbol(l, $5);
		  $$ = _symbol_create_list(SQL_IF, l);
		}
		  
	;

if_opt_else:
	/* empty */ 	
		{ $$ = NULL; }
   |	ELSE procedure_statement_list 
	  	{ $$ = _symbol_create_list(SQL_ELSE, $2); }
   |	ELSEIF search_condition THEN procedure_statement_list 
	if_opt_else
		{ dlist *l = L();
		  append_symbol(l, $2);
		  append_list(l, $4);
		  append_symbol(l, $5);
		  { $$ = _symbol_create_list(SQL_IF, l); }
		}
	;

while_statement:
	opt_begin_label
	WHILE search_condition DO
	procedure_statement_list
	END WHILE opt_end_label

		{ dlist *l;
		  char *label = $1?$1:$8;
		  if ($1 && $8 && strcmp($1, $8) != 0) {
			$$ = NULL;
			yyerror("Labels of while should match");
			YYABORT;
			YYERROR; /* just to silence intel compiler */
		  }
 		  l = L();
		  append_symbol(l, $3); /* condition */
		  append_list(l, $5);	/* statements */
		  append_string(l, label);
		  $$ = _symbol_create_list(SQL_WHILE, l);
		}
 ;

opt_begin_label:
	/* empty */ 	{ $$ = NULL; }
 |	ident ':'
 ;

opt_end_label:
	/* empty */ 	{ $$ = NULL; }
 |	ident 
 ;
	
	
table_function_column_list:
	column data_type	{ $$ = L();
				  append_string($$, $1);
			  	  append_type($$, &$2);
				}
  |     table_function_column_list ',' column data_type
				{ 
				  append_string($$, $3);
			  	  append_type($$, &$4);
				}
  ;

func_data_type:
    TABLE '(' table_function_column_list ')'	
		{ $$ = _symbol_create_list(SQL_TABLE, $3); }
 |  data_type
		{ $$ = _symbol_create_list(SQL_TYPE, append_type(L(),&$1)); }
 ;

opt_paramlist:
    paramlist
 |			{ $$ = NULL; }
 ;

paramlist:
    paramlist ',' ident data_type
			{ dlist *p = L();
			  append_string(p, $3);
			  append_type(p, &$4);
			  $$ = append_list($1, p); }
 |  ident data_type
			{ dlist *l = L();
			  dlist *p = L();
			  append_string(p, $1);
			  append_type(p, &$2);
			  $$ = append_list(l, p); }
 ;

/*
Define triggered SQL-statements.

  <trigger definition>    ::=
         CREATE TRIGGER <trigger name> <trigger action time> <trigger event>
         ON <table name> [ REFERENCING <old or new values alias list> ]
         <triggered action>

  <trigger action time>    ::=   BEFORE | AFTER

  <trigger event>    ::=   INSERT | DELETE | UPDATE [ OF <trigger column list> ]

  <trigger column list>    ::=   <column name list>

  <triggered action>    ::=
         [ FOR EACH { ROW | STATEMENT } ]
         [ WHEN <left paren> <search condition> <right paren> ]
         <triggered SQL statement>

  <triggered SQL statement>    ::=
         <SQL procedure statement>
     |     BEGIN ATOMIC { <SQL procedure statement> <semicolon> }... END

  <old or new values alias list>    ::=   <old or new values alias> ...

  <old or new values alias>    ::=
         OLD [ ROW ] [ AS ] <old values correlation name>
     |   NEW [ ROW ] [ AS ] <new values correlation name>
     |   OLD TABLE [ AS ] <old values table alias>
     |   NEW TABLE [ AS ] <new values table alias>

  <old values table alias>    ::=   <identifier>

  <new values table alias>    ::=   <identifier>

  <old values correlation name>    ::=   <correlation name>

  <new values correlation name>    ::=   <correlation name>
*/

trigger_def:
    create TRIGGER qname trigger_action_time trigger_event
    ON ident opt_referencing_list triggered_action
	{ dlist *l = L();
	  append_list(l, $3);
	  append_int(l, $4);
	  append_symbol(l, $5);
	  append_string(l, $7);
	  append_list(l, $8);
	  append_list(l, $9);
	  $$ = _symbol_create_list(SQL_CREATE_TRIGGER, l); 
	}
 ;

trigger_action_time:
    BEFORE	{ $$ = 0; }
 |  AFTER	{ $$ = 1; }
 ;

trigger_event:
    INSERT 			{ $$ = _symbol_create_list(SQL_INSERT, NULL); }
 |  sqlDELETE 			{ $$ = _symbol_create_list(SQL_DELETE, NULL); }
 |  UPDATE 			{ $$ = _symbol_create_list(SQL_UPDATE, NULL); }
 |  UPDATE OF ident_commalist 	{ $$ = _symbol_create_list(SQL_UPDATE, $3); }
 ;

opt_referencing_list:
    /* empty */ 				{ $$ = NULL; }
 |  REFERENCING old_or_new_values_alias_list 	{ $$ = $2; }
 ;

old_or_new_values_alias_list:
    old_or_new_values_alias	{ $$ = append_list(L(), $1); }
 |  old_or_new_values_alias_list 
    old_or_new_values_alias	{ $$ = append_list($1, $2); } 
 ;
       
old_or_new_values_alias:
	/* is really a correlation name */
       OLD opt_row opt_as ident	{ $$ = append_string(append_int(L(), 0), $4); }
 |  sqlNEW opt_row opt_as ident	{ $$ = append_string(append_int(L(), 1), $4); }
 |     OLD TABLE opt_as ident 	{ $$ = append_string(append_int(L(), 0), $4); }
 |  sqlNEW TABLE opt_as ident 	{ $$ = append_string(append_int(L(), 1), $4); }
 ;

opt_as:
    /* empty */
 |  AS
 ;

opt_row:
    /* empty */
 |  ROW	
 ;

triggered_action:
    opt_for_each opt_when triggered_statement
	{ $$ = L();
	  append_int($$, $1);
	  append_symbol($$, $2);
	  append_list($$, $3);
	}

opt_for_each:
    /* default for each statement */ 	{ $$ = 1; }
 |  FOR EACH row_or_statement 		{ $$ = $3; }
 ;

row_or_statement:
    ROW 	{ $$ = 0; }
 |  STATEMENT 	{ $$ = 1; }
 ;

opt_when:
    /* empty */ 			{ $$ = NULL; }
 |  WHEN  search_condition  	{ $$ = $2; }
 ;

triggered_statement:
    trigger_procedure_statement		
				{ $$ = append_symbol(L(), $1); }
 |  BEGIN ATOMIC 
    trigger_procedure_statement_list 
    END 			{ $$ = $3; }
 ;

drop_statement:
    DROP TABLE qname drop_action

	{ dlist *l = L();
	  append_list(l, $3 );
	  append_int(l, $4 );
	  $$ = _symbol_create_list( SQL_DROP_TABLE, l ); }
 | DROP FUNCTION qname drop_action
	{ dlist *l = L();
	  append_list(l, $3 );
	  append_int(l, $4 );
	  $$ = _symbol_create_list( SQL_DROP_FUNC, l ); }
 |  DROP VIEW qname	  { $$ = _symbol_create_list( SQL_DROP_VIEW, $3 ); }
 |  DROP ROLE qname	  { $$ = _symbol_create_list( SQL_DROP_ROLE, $3 ); }
 |  DROP USER ident	  { $$ = _symbol_create( SQL_DROP_USER, $3 ); }
 |  DROP INDEX qname	  { $$ = _symbol_create_list( SQL_DROP_INDEX, $3 ); }
 |  DROP TRIGGER qname	  { $$ = _symbol_create_list( SQL_DROP_TRIGGER, $3 ); }
 ;

drop_action:
    /* empty */		{ $$ = 0; }
 |  RESTRICT		{ $$ = 0; }
 |  CASCADE		{ $$ = 1; }
 ;
	/* data manipulative stmts */

sql:
   transaction_statement
 | update_statement
 | sql_connections_statement
 ;

update_statement: 
/* todo merge statement */
   delete_stmt
 | insert_stmt
 | update_stmt
 | copyfrom_stmt
 ;

sql_connections_statement:
   connect_stmt
 | disconnect_stmt
;
transaction_statement:
   _transaction_stmt
	{ mvc *m = (mvc*)parm;
	  $$ = $1;
	  m->type = Q_TRANS;					}
 ;

_transaction_stmt:
    COMMIT opt_work opt_chain
		{ $$ = _symbol_create_int( TR_COMMIT, $3);  }
 |  SAVEPOINT ident
		{ $$ = _symbol_create( TR_SAVEPOINT, $2); }
 |  RELEASE SAVEPOINT ident
		{ $$ = _symbol_create( TR_RELEASE, $3); }
 |  ROLLBACK opt_work opt_chain opt_to_savepoint
		{ $$ = _symbol_create_list( TR_ROLLBACK,
		   append_string(
			append_int(L(), $3), $4 )); }
 |  START TRANSACTION transaction_mode_list
		{ $$ = _symbol_create_int( TR_START, $3); }
 |  SET LOCAL TRANSACTION transaction_mode_list
		{ $$ = _symbol_create_int( TR_MODE, $4); }
 |  SET TRANSACTION transaction_mode_list
		{ $$ = _symbol_create_int( TR_MODE, $3); }
 ;

transaction_mode_list:
	/* empty */		{ $$ = tr_none; }
 |	_transaction_mode_list
 ;

_transaction_mode_list:
	transaction_mode
		{ $$ = $1; }
 |	_transaction_mode_list ',' transaction_mode
		{ $$ = ($1 | $3); }
 ;


transaction_mode:
	READ ONLY			{ $$ = tr_readonly; }
 |	READ WRITE			{ $$ = tr_writable; }
 |	ISOLATION LEVEL iso_level	{ $$ = tr_serializable; }
 |	DIAGNOSTICS sqlSIZE intval	{ $$ = tr_none; /* not supported */ }
 ;

iso_level:
	READ UNCOMMITTED
 |	READ COMMITTED
 |	REPEATABLE READ
 |	SERIALIZABLE
 ;

opt_work: /* pure syntax sugar */
    WORK		{ $$ = 0; }
 |  /* empty */		{ $$ = 0; }
 ;

opt_chain:
    AND CHAIN		{ $$ = 1; }
 |  AND NO CHAIN	{ $$ = 0; }
 |  /* empty */		{ $$ = 0; }
 ;

opt_to_savepoint:
    /* empty */		{ $$ = NULL; }
 |  TO SAVEPOINT ident  { $$ = $3; }
 ;

copyfrom_stmt:
    COPY opt_nr INTO qname FROM string_commalist opt_seps
	{ dlist *l = L();
	  append_list(l, $4);
	  append_list(l, $6);
	  append_list(l, $7);
	  append_int(l, $2);
	  $$ = _symbol_create_list( SQL_COPYFROM, l ); }
  | COPY opt_nr INTO qname FROM STDIN opt_seps
	{ dlist *l = L();
	  append_list(l, $4);
	  append_list(l, NULL);
	  append_list(l, $7);
	  append_int(l, $2);
	  $$ = _symbol_create_list( SQL_COPYFROM, l ); }
  ;

opt_seps:
    /* empty */
				{ dlist *l = L();
				  append_string(l, sa_strdup(SA, "|"));
				  append_string(l, sa_strdup(SA, "\\n"));
				  $$ = l; }
 |  opt_using DELIMITERS string
				{ dlist *l = L();
				  append_string(l, $3);
				  append_string(l, sa_strdup(SA, "\\n"));
				  $$ = l; }
 |  opt_using DELIMITERS string ',' string
				{ dlist *l = L();
				  append_string(l, $3);
				  append_string(l, $5);
				  $$ = l; }
 ;

opt_using:
    /* empty */			{ $$ = NULL; }
 |  USING			{ $$ = NULL; }
 ;

opt_nr:
    /* empty */			{ $$ = -1; }
 |  intval RECORDS		{ $$ = $1; }
 ;

string_commalist:
    string		{ $$ = append_string(L(), $1); }
 |  string_commalist ',' string
			{ $$ = append_string($1, $3); }
 ;

delete_stmt:
    sqlDELETE FROM qname opt_where_clause

	{ dlist *l = L();
	  append_list(l, $3);
	  append_symbol(l, $4);
	  $$ = _symbol_create_list( SQL_DELETE, l ); }
 ;

update_stmt:
    UPDATE qname SET assignment_commalist opt_where_clause

	{ dlist *l = L();
	  append_list(l, $2);
	  append_list(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_UPDATE, l ); }
 ;

connect_stmt:
    CONNECT TO DEFAULT		{ $$ = _symbol_create_list(SQL_CONNECT, L()); }
 |  CONNECT TO string opt_port DATABASE string opt_db_alias user_passwd opt_lang

	{ dlist *l = L();
	  append_string(l, $3);
	  append_int(l, $4);
	  append_string(l, $6);
	  append_string(l, $7);
	  append_symbol(l, $8);
	  append_string(l, $9);
	  $$ = _symbol_create_list( SQL_CONNECT, l ); }
 ;

disconnect_stmt:
    DISCONNECT string		{ dlist *l = L(); 
				 append_string(l, $2);
				 append_int(l, 0);
				 $$ = _symbol_create_list(SQL_DISCONNECT, l);
				}
 |  DISCONNECT ALL		{ dlist *l = L();
				 $$ = _symbol_create_list(SQL_DISCONNECT, l);
				}
 ;

opt_port:
    /*empty*/		{ $$ = 0; }
 |  PORT intval		{ $$ = $2; }
 ;

opt_db_alias:
    /*empty*/		{ $$ = NULL; }
 |  AS string		{ $$ = $2; }
 ;

user_passwd:
 USER string PASSWORD string		{ dlist *l = L(); 
					append_string(l, $2);
					append_string(l, $4);
					$$ = _symbol_create_list(SQL_USER, l);
					}
 ;

opt_lang:
    /*empty*/		{ $$ = NULL; }
 |  LANGUAGE string	{ $$ = $2; }
 ;

/* todo merge statment 
--p 
Conditionally update rows of a table, or insert new rows into a table, or both.
--/p

<merge statement> ::=
		MERGE INTO <target table> [ [ AS ] <merge correlation name> ]
		USING <table reference> ON <search condition> <merge operation specification>

<merge correlation name> ::= <correlation name>

<merge operation specification> ::= <merge when clause>...

<merge when clause> ::= <merge when matched clause> | <merge when not matched clause>

<merge when matched clause> ::= WHEN MATCHED THEN <merge update specification>

<merge when not matched clause> ::= WHEN NOT MATCHED THEN <merge insert specification>

<merge update specification> ::= UPDATE SET <set clause list>

<merge insert specification> ::=
		INSERT [ <left paren> <insert column list> <right paren> ]
		[ <override clause> ] VALUES <merge insert value list>

<merge insert value list> ::=
		<left paren> <merge insert value element> [ { <comma> <merge insert value element> }... ] <right paren>

<merge insert value element> ::= <value expression> | <contextually typed value specification>
*/

insert_stmt:
    INSERT INTO qname values_or_query_spec

	{ dlist *l = L();
	  append_list(l, $3);
	  append_list(l, NULL);
	  append_symbol(l, $4);
	  $$ = _symbol_create_list( SQL_INSERT, l ); }

 |  INSERT INTO qname column_commalist_parens values_or_query_spec

	{ dlist *l = L();
	  append_list(l, $3);
	  append_list(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_INSERT, l ); }
 ;

values_or_query_spec:
/* empty values list */
		{ $$ = _symbol_create_list( SQL_VALUES, L()); }
 |   VALUES row_commalist
		{ $$ = _symbol_create_list( SQL_VALUES, $2); }
 |  select_no_parens
 ;


row_commalist:
    '(' atom_commalist ')'	{ $$ = append_list(L(), $2); }
 |  row_commalist ',' '(' atom_commalist ')'
				{ $$ = append_list($1, $4); }
 ;

atom_commalist:
    insert_atom		{ $$ = append_symbol(L(), $1); }
 |  atom_commalist ',' insert_atom
			{ $$ = append_symbol($1, $3); }
 ;

null:
   NULLX
	 { 
	   mvc *m = (mvc*)parm;

	   if (CACHE_MODE(m->mode) && 
	      (!m->argfixed || m->argc < MAXPARAMS-2) && m->cache){
		/* replace by argument */
		atom *a = atom_general( sql_bind_localtype("str"), NULL, 0);

		sql_add_arg( m, a);
		$$ = _symbol_create_list( SQL_COLUMN,
			append_int(L(), m->argc-1));
	   } else {
		$$ = _symbol_create(SQL_NULL, NULL );
	   }
	}
 ;


simple_atom:
    scalar_exp
 |  null
 ;

insert_atom:
    simple_atom
 |  simple_select
 |  set_select
 ;

opt_distinct:
    /* empty */		{ $$ = FALSE; }
 |  ALL			{ $$ = FALSE; }
 |  DISTINCT		{ $$ = TRUE; }
 ;

assignment_commalist:
    assignment		{ $$ = append_symbol(L(), $1 ); }
 |  assignment_commalist ',' assignment
			{ $$ = append_symbol($1, $3 ); }
 ;

assignment:
    column '=' scalar_exp

	{ dlist *l = L();
	  append_string(l, $1);
	  append_symbol(l, $3);
	  $$ = _symbol_create_list( SQL_ASSIGN, l); }

 |  column '=' search_condition

	{ dlist *l = L();
	  append_string(l, $1);
	  append_symbol(l, $3 );
	  $$ = _symbol_create_list( SQL_ASSIGN, l); }

 |  column '=' NULLX

	{ dlist *l = L();
	  append_string(l, $1);
	  append_symbol(l, NULL );
	  $$ = _symbol_create_list( SQL_ASSIGN, l); }
 ;

opt_where_clause:
    /* empty */			{ $$ = NULL; }
 |  WHERE search_condition	{ $$ = $2; }
 ;

	/* query expressions */

joined_table:
   '(' joined_table ')'
	{ $$ = $2; }
 |  table_ref CROSS JOIN table_ref

	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_symbol(l, $4);
	  $$ = _symbol_create_list( SQL_CROSS, l); }
 |  table_ref UNIONJOIN table_ref join_spec
	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, 0);
	  append_int(l, 4);
	  append_symbol(l, $3);
	  append_symbol(l, $4);
	  $$ = _symbol_create_list( SQL_UNIONJOIN, l); }
 |  table_ref JOIN table_ref join_spec
	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, 0);
	  append_int(l, 0);
	  append_symbol(l, $3);
	  append_symbol(l, $4);
	  $$ = _symbol_create_list( SQL_JOIN, l); }
 |  table_ref NATURAL JOIN table_ref
	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, 1);
	  append_int(l, 0);
	  append_symbol(l, $4);
	  append_symbol(l, NULL);
	  $$ = _symbol_create_list( SQL_JOIN, l); }
 |  table_ref join_type JOIN table_ref join_spec
	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, 0);
	  append_int(l, $2);
	  append_symbol(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_JOIN, l); }
 |  table_ref NATURAL join_type JOIN table_ref
	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, 1);
	  append_int(l, $3);
	  append_symbol(l, $5);
	  append_symbol(l, NULL);
	  $$ = _symbol_create_list( SQL_JOIN, l); }
  ;

join_type:
    INNER			{ $$ = 0; }
  | outer_join_type OUTER	{ $$ = 1 + $1; }
  | outer_join_type		{ $$ = 1 + $1; }
  ;

outer_join_type:
    LEFT		{ $$ = 0; }
  | RIGHT		{ $$ = 1; }
  | FULL		{ $$ = 2; }
  ;

join_spec:
    ON search_condition			{ $$ = $2; }
  | USING column_commalist_parens
		{ $$ = _symbol_create_list( SQL_USING, $2); }
  ;

/*
<query expression> ::= [ <with clause> ] <query expression body>

<with clause> ::= WITH [ RECURSIVE ] <with list>

<with list> ::= <with list element> [ { <comma> <with list element> }... ]

<with list element> ::=
                <query name> [ <left paren> <with column list> <right paren> ]
                AS <left paren> <query expression> <right paren> [ <search or cycle clause> ]

<with column list> ::= <column name list>

RECURSIVE and <search or cycle clause> are currently not supported
*/
sql:
	WITH with_list query_expression
	{
		dlist *l = L();
	  	append_list(l, $2);
	  	append_symbol(l, $3);
	  	$$ = _symbol_create_list( SQL_WITH, l ); 
	}
 ;

with_list:
	with_list ',' with_list_element	 { $$ = append_symbol($1, $3); }
 |	with_list_element	 	 { $$ = append_symbol(L(), $1); }
 ;

with_list_element: 
    ident opt_column_list AS '(' query_expression ')'
	{  dlist *l = L();
	  append_list(l, append_string(L(), $1));
	  append_list(l, $2);
	  append_symbol(l, $5);
	  append_int(l, FALSE);	/* no with check */
	  append_int(l, FALSE);	/* inlined view  (ie not persistent) */
	  $$ = _symbol_create_list( SQL_CREATE_VIEW, l ); 
	}
 ;

sql:
    select_no_parens
 |  '(' select_no_parens ')' 	{ $$ = $2; }
 ;

select_with_parens:
    '(' select_no_parens ')'	{ $$ = $2; }
 |  '(' select_with_parens ')'	{ $$ = $2; }
 ;

simple_select:
    SELECT opt_distinct selection table_exp
		opt_order_by_clause opt_limit opt_offset
	{ $$ = newSelectNode( SA, $2, $3, NULL,
		$4->h->data.sym,
		$4->h->next->data.sym,
		$4->h->next->next->data.sym,
		$4->h->next->next->next->data.sym,
		$5, NULL, $6, $7);
	}
    ;

select_statement_single_row:
    SELECT opt_distinct selection INTO select_target_list table_exp
	{ $$ = newSelectNode( SA, $2, $3, $5,
		$6->h->data.sym,
		$6->h->next->data.sym,
		$6->h->next->next->data.sym,
		$6->h->next->next->next->data.sym,
		NULL, NULL, -1, -1);
	}
    ;

select_target_list:
	target_specification 	{ $$ = append_string(L(), $1); }
 |  	select_target_list ',' target_specification
				{ $$ = append_string($1, $3); }
 ;

target_specification:
	ident
 ;

select_no_parens:
    simple_select
 |  set_select 
 ;

complex_select:
    select_no_parens
 |  select_with_parens
 ;

set_select:

    complex_select UNION
	set_distinct opt_corresponding complex_select

	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, $3);
	  append_list(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_UNION, l); }

 |  complex_select EXCEPT set_distinct opt_corresponding complex_select

	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, $3);
	  append_list(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_EXCEPT, l); }

 |  complex_select INTERSECT set_distinct opt_corresponding complex_select

	{ dlist *l = L();
	  append_symbol(l, $1);
	  append_int(l, $3);
	  append_list(l, $4);
	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_INTERSECT, l); }
 ;

set_distinct:
    /* empty */		{ $$ = TRUE; }
 |  ALL			{ $$ = FALSE; }
 |  DISTINCT		{ $$ = TRUE; }
 ;


opt_corresponding:
	/* empty */	{ $$ = NULL; }
 |  CORRESPONDING
			{ $$ = L(); }
 |  CORRESPONDING BY '(' column_ref_commalist ')'
			{ $$ = $4; }
 ;

selection:
    column_exp_commalist
 |  '*' 			{ $$ = NULL; }
 ;

table_exp:
    opt_from_clause opt_where_clause opt_group_by_clause opt_having_clause

	{ $$ = L();
	  append_symbol($$, $1);
	  append_symbol($$, $2);
	  append_symbol($$, $3);
	  append_symbol($$, $4); }
 ;

opt_from_clause:
    /* empty */			 { $$ = NULL; }
 |  FROM table_ref_commalist 	 { $$ = _symbol_create_list( SQL_FROM, $2); }
 ;

table_ref_commalist:
    table_ref		{ $$ = append_symbol(L(), $1); }
 |  table_ref_commalist ',' table_ref
			{ $$ = append_symbol($1, $3); }
 ;

simple_table:
   qname 			{ dlist *l = L();
		  		  append_list(l, $1);
		  	  	  append_symbol(l, NULL);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 | qname table_name 		{ dlist *l = L();
		  		  append_list(l, $1);
		  	  	  append_symbol(l, $2);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 | func_ref table_name		{ dlist *l = L();
		  		  append_symbol(l, $1);
		  	  	  append_symbol(l, $2);
		  		  $$ = _symbol_create_list(SQL_TABLE, l); }
 | func_ref 			{ dlist *l = L();
		  		  append_symbol(l, $1);
		  	  	  append_symbol(l, NULL);
		  		  $$ = _symbol_create_list(SQL_TABLE, l); }
 ;

table_ref:
    simple_table
 |  select_with_parens table_name
				{
				  $$ = $1;
				  if ($$->token == SQL_SELECT) {
				  	SelectNode *sn = (SelectNode*)$1;
				  	sn->name = $2;
				  } else {
				  	append_symbol($1->data.lval, $2);
				  }
				}
 |  select_with_parens
				{ $$ = NULL;
				  yyerror("subquery table reference needs alias, use AS xxx");
				  YYABORT;
				}
 |  joined_table 		{ $$ = $1;
				  append_symbol($1->data.lval, NULL); }
/* the following rule seems to be redundant ?  
 |  '(' joined_table ')' table_name
				{ $$ = $2;
				  append_symbol($2->data.lval, $4); }
*/
 ;

table_name:
    AS ident '(' name_commalist ')'
				{ dlist *l = L();
		  		  append_string(l, $2);
		  	  	  append_list(l, $4);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 |  AS ident
				{ dlist *l = L();
		  		  append_string(l, $2);
		  	  	  append_list(l, NULL);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 |  ident '(' name_commalist ')'
				{ dlist *l = L();
		  		  append_string(l, $1);
		  	  	  append_list(l, $3);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 |  ident
				{ dlist *l = L();
		  		  append_string(l, $1);
		  	  	  append_list(l, NULL);
		  		  $$ = _symbol_create_list(SQL_NAME, l); }
 ;

opt_group_by_clause:
    /* empty */ 		  { $$ = NULL; }
 |  GROUP BY column_ref_commalist { $$ = _symbol_create_list( SQL_GROUPBY, $3 );}
 ;

column_ref_commalist:
    column_ref		{ $$ = append_symbol(L(),
			       _symbol_create_list(SQL_COLUMN,$1)); }
 |  column_ref_commalist ',' column_ref
			{ $$ = append_symbol( $1,
			       _symbol_create_list(SQL_COLUMN,$3)); }
 ;

opt_having_clause:
    /* empty */ 		 { $$ = NULL; }
 |  HAVING search_condition	 { $$ = $2; }
 ;


search_condition:
    search_condition OR search_condition
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_symbol(l, $3);
		  $$ = _symbol_create_list(SQL_OR, l ); }
 |  search_condition AND search_condition
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_symbol(l, $3);
		  $$ = _symbol_create_list(SQL_AND, l ); }
 |  '(' search_condition ')'	{ $$ = $2; }
 | NOT search_condition { 
			  $$ = $2;

			  if ($$->token == SQL_EXISTS)
				$$->token = SQL_NOT_EXISTS;
			  else
				$$ = _symbol_create_symbol(SQL_NOT, $2);
			}
	
 |  predicate
 |  BOOL_FALSE
		{ sql_subtype t;
		  sql_find_subtype(&t, "boolean", 0, 0 );
		  $$ = _newAtomNode( atom_bool(&t, FALSE)); }
 |  BOOL_TRUE
		{ sql_subtype t;
		  sql_find_subtype(&t, "boolean", 0, 0 );
		  $$ = _newAtomNode( atom_bool(&t, TRUE)); }
 ;

 

opt_order_by_clause:
    /* empty */ 			  { $$ = NULL; }
 |  ORDER BY sort_specification_list
		{ $$ = _symbol_create_list( SQL_ORDERBY, $3); }
 ;

opt_limit:
    /* empty */ 			{ $$ = -1; }
 |  LIMIT nonzero			{ $$ = $2; }
 ;

opt_offset:
	/* empty */				{ $$ = -1; }
 |  OFFSET posint			{ $$ = $2; }
 ;

sort_specification_list:
    ordering_spec	 { $$ = append_symbol(L(), $1); }
 |  sort_specification_list ',' ordering_spec
			 { $$ = append_symbol( $1, $3 ); }
 ;

ordering_spec:
    column_ref opt_asc_desc
	{ dlist *l = L();
	  append_symbol(l, _symbol_create_list(SQL_COLUMN, $1));
	  append_int(l, $2);
	  $$ = _symbol_create_list(SQL_COLUMN, l ); }

 ;

opt_asc_desc:
    /* empty */ 	{ $$ = TRUE; }
 |  ASC			{ $$ = TRUE; }
 |  DESC		{ $$ = FALSE; }
 ;

predicate:
    comparison_predicate
 |  between_predicate
 |  like_predicate
 |  test_for_null
 |  in_predicate
 |  all_or_any_predicate
 |  existence_test
 |  '(' predicate ')' 	{  $$ = $2; }
 ;

pred_exp:
    scalar_exp
 |  predicate
 ;

comparison_predicate:
    pred_exp COMPARISON pred_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_string(l, $2);
		  append_symbol(l, $3);
		  $$ = _symbol_create_list(SQL_COMPARE, l ); }
 |  pred_exp '=' pred_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_string(l, sa_strdup(SA, "="));
		  append_symbol(l, $3);
		  $$ = _symbol_create_list(SQL_COMPARE, l ); }
 ;

between_predicate:
    pred_exp NOT BETWEEN opt_bounds pred_exp AND pred_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_int(l, $4);
		  append_symbol(l, $5);
		  append_symbol(l, $7);
		  $$ = _symbol_create_list(SQL_NOT_BETWEEN, l ); }
 |  pred_exp BETWEEN opt_bounds pred_exp AND pred_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_int(l, $3);
		  append_symbol(l, $4);
		  append_symbol(l, $6);
		  $$ = _symbol_create_list(SQL_BETWEEN, l ); }
 ;

opt_bounds:
   /* empty */ 	{ $$ = 0; }
 | ASYMMETRIC 	{ $$ = 0; }
 | SYMMETRIC 	{ $$ = 1; }
 ;

like_predicate:
    pred_exp NOT LIKE atom_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_symbol(l, $4);
		  $$ = _symbol_create_list(SQL_NOT_LIKE, l ); }
 |  pred_exp LIKE atom_exp
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_symbol(l, $3);
		  $$ = _symbol_create_list(SQL_LIKE, l ); }
 ;

atom2:
	'(' atom ')' 	{ $$ = $2; }
 |	atom
 ;

atom_exp:
    atom2
	{ dlist *l = L();
	  append_symbol(l, $1);
	  $$ = _symbol_create_list(SQL_ESCAPE, l ); }
 |  atom2 ESCAPE string
 	{ char *s = sql2str($3);
	  if (_strlen(s) != 1) {
		char buf[BUFSIZ];
		snprintf(buf, BUFSIZ, "ESCAPE must be one character");
		yyerror(buf);
		$$ = NULL;
		YYABORT;
	  } else {
		dlist *l = L();
		append_symbol(l, $1);
		append_string(l, s);
		$$ = _symbol_create_list(SQL_ESCAPE, l);
	  }
	}
 |  '?'	
	{ dlist *l = L();
	  append_symbol(l, _symbol_create( SQL_PARAMETER, NULL ));
	  $$ = _symbol_create_list(SQL_ESCAPE, l ); }
 ;

test_for_null:
    column_ref IS NOT NULLX  { symbol *s =_symbol_create_list(SQL_COLUMN, $1);
			      $$ = _symbol_create_symbol( SQL_NOT_NULL, s ); }
 |  column_ref IS NULLX	     { symbol *s =_symbol_create_list(SQL_COLUMN, $1);
			       $$ = _symbol_create_symbol( SQL_NULL, s ); }
 ;

in_predicate:
    pred_exp NOT sqlIN '(' atom_commalist ')'
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_list(l, $5);
		  $$ = _symbol_create_list(SQL_NOT_IN, l ); }
 |  pred_exp sqlIN '(' atom_commalist ')'
		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_list(l, $4);
		  $$ = _symbol_create_list(SQL_IN, l ); }
 ;

all_or_any_predicate:
    pred_exp COMPARISON any_all_some subquery

		{ dlist *l = L();
		  append_symbol(l, $1);
		  append_string(l, $2);
		  append_string(l, $3);
		  append_symbol(l, $4);
		  $$ = _symbol_create_list(SQL_COMPARE, l ); }
 ;

any_all_some:
    ANY		{ $$ = $1; }
 |  ALL		{ $$ = $1; }
 |  SOME	{ $$ = $1; }
 ;

existence_test:
    EXISTS subquery 	{ $$ = _symbol_create_symbol( SQL_EXISTS, $2 ); }
/*|  NOT EXISTS subquery { $$ = _symbol_create_symbol( SQL_NOT_EXISTS, $3 ); }*/
 ;

subquery:
	select_with_parens
 ;

	/* simple_scalar expressions */
simple_scalar_exp:
    value_exp
 |  scalar_exp '+' scalar_exp
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "sql_add")));
	  		  append_symbol(l, $1);
	  		  append_symbol(l, $3);
	  		  $$ = _symbol_create_list( SQL_BINOP, l ); }
 |  scalar_exp '-' scalar_exp
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "sql_sub")));
	  		  append_symbol(l, $1);
	  		  append_symbol(l, $3);
	  		  $$ = _symbol_create_list( SQL_BINOP, l ); }
 |  scalar_exp '*' scalar_exp
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "sql_mul")));
	  		  append_symbol(l, $1);
	  		  append_symbol(l, $3);
	  		  $$ = _symbol_create_list( SQL_BINOP, l ); }
 |  scalar_exp '/' scalar_exp
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "sql_div")));
	  		  append_symbol(l, $1);
	  		  append_symbol(l, $3);
	  		  $$ = _symbol_create_list( SQL_BINOP, l ); }
 |  scalar_exp '%' scalar_exp
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "mod")));
	  		  append_symbol(l, $1);
	  		  append_symbol(l, $3);
	  		  $$ = _symbol_create_list( SQL_BINOP, l ); }
 |  '+' scalar_exp %prec UMINUS 
			{ $$ = $2; }
 |  '-' scalar_exp %prec UMINUS 
			{ dlist *l = L();
			  append_list(l, 
			  	append_string(L(), sa_strdup(SA, "sql_neg")));
	  		  append_symbol(l, $2);
	  		  $$ = _symbol_create_list( SQL_UNOP, l ); }
 |  '(' scalar_exp ')' 	{ $$ = $2; }
 ;

scalar_exp:
    simple_scalar_exp 	{ $$ = $1; }
 |  subquery	%prec UMINUS
 ;

value_exp:
    atom
 |  user		{ $$ = _symbol_create_list( SQL_COLUMN, 
			  append_string(L(), sa_strdup(SA, "current_user"))); }
 |  CURRENT_ROLE	{ $$ = _symbol_create_list( SQL_COLUMN, 
			  append_string(L(), sa_strdup(SA, "current_role"))); }
 |  window_function
 |  column_ref 		{ $$ = _symbol_create_list( SQL_COLUMN, $1); }
 |  var_ref		
 |  aggr_ref
 |  func_ref
 |  NEXT VALUE FOR ident	{ $$ = _symbol_create( SQL_NEXT, $4); }
 |  datetime_funcs
 |  string_funcs
 |  case_exp
 |  cast_exp
 |  '?'			{ $$ = _symbol_create( SQL_PARAMETER, NULL ); }
 ;

/*
<window function> ::= <window function type> OVER <window name or specification>

<window function type> ::=
		<rank function type> <left paren> <right paren>
	|	ROW_NUMBER <left paren> <right paren>
	|	<aggregate function>

<rank function type> ::= RANK | DENSE_RANK | PERCENT_RANK | CUME_DIST

<window name or specification> ::= <window name> | <in-line window specification>

<in-line window specification> ::= <window specification>


<window specification> ::= <left paren> <window specification details> <right paren>

<window specification details> ::=
                [ <existing window name> ] [ <window partition clause> ] [ <window order clause> ] [ <window frame clause> ]

<existing window name> ::= <window name>

<window partition clause> ::= PARTITION BY <window partition column reference list>

<window partition column reference list> ::= <window partition column reference> [ { <comma> <window partition column reference> }... ]

<window partition column reference> ::= <column reference> [ <collate clause> ]

<window order clause> ::= ORDER BY <sort specification list>

<window frame clause> ::= <window frame units> <window frame extent> [ <window frame exclusion> ]
*/

window_function: 
	window_function_type OVER '(' window_specification ')'
	{ $$ = _symbol_create_list( SQL_RANK, 
		append_list(append_symbol(L(), $1), $4)); }
  ;

window_function_type:
	RANK '(' ')' 	{ $$ = _symbol_create( SQL_RANK, $1 ); }
  |	aggr_ref
  ;

window_specification:
	window_partition_clause window_order_clause
	{ $$ = append_symbol(append_symbol(L(), $1), $2); }
  ;

window_partition_clause:
	/* empty */ 	{ $$ = NULL; }
  |	PARTITION BY column_ref_commalist
	{ $$ = _symbol_create_list( SQL_GROUPBY, $3 ); }
  ;

window_order_clause:
	/* empty */ 	{ $$ = NULL; }
  |	ORDER BY sort_specification_list
	{ $$ = _symbol_create_list( SQL_ORDERBY, $3 ); }
  ;

var_ref:
	AT ident 	{ $$ = _symbol_create( SQL_NAME, $2 ); }
 ;

func_ref:
    qfunc '(' ')'
	{ dlist *l = L();
  	  append_list(l, $1);
	  $$ = _symbol_create_list( SQL_OP, l ); }
|   qfunc '(' scalar_exp ')'
	{ dlist *l = L();
  	  append_list(l, $1);
  	  append_symbol(l, $3);
	  $$ = _symbol_create_list( SQL_UNOP, l ); }
|   qfunc '(' scalar_exp ',' scalar_exp ')'
	{ dlist *l = L();
  	  append_list(l, $1);
  	  append_symbol(l, $3);
  	  append_symbol(l, $5);
	  $$ = _symbol_create_list( SQL_BINOP, l ); }
|   qfunc '(' scalar_exp_list2 ')'
	{ dlist *l = L();
  	  append_list(l, $1);
  	  append_list(l, $3);
	  $$ = _symbol_create_list( SQL_NOP, l ); }
 ;

qfunc:
	func_ident		{ $$ = append_string(L(), $1); }
 |      ident '.' func_ident	{ $$ = append_string(
					append_string(L(), $1), $3);}
 ;

func_ident:
	IDENT 	{ $$ = $1; }
 |	LEFT	{ $$ = sa_strdup(SA, "left"); }
 |	RIGHT	{ $$ = sa_strdup(SA, "right"); }
 |	INSERT	{ $$ = sa_strdup(SA, "insert"); }
 ;

datetime_funcs:
    EXTRACT '(' datetime_field FROM scalar_exp ')'
			{ dlist *l = L();
			  const char *ident = datetime_field((itype)$3);
			  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, ident)));
  		  	  append_symbol(l, $5);
		  	  $$ = _symbol_create_list( SQL_UNOP, l ); }
 |  CURRENT_DATE opt_brackets
 			{ dlist *l = L();
			  append_list(l,
			  	append_string(L(), sa_strdup(SA, "current_date")));
	  		  $$ = _symbol_create_list( SQL_OP, l ); }
 |  CURRENT_TIME opt_brackets
 			{ dlist *l = L();
			  append_list(l,
			  	append_string(L(), sa_strdup(SA, "current_time")));
	  		  $$ = _symbol_create_list( SQL_OP, l ); }
 |  CURRENT_TIMESTAMP opt_brackets
 			{ dlist *l = L();
			  append_list(l,
			  	append_string(L(), sa_strdup(SA, "current_timestamp")));
	  		  $$ = _symbol_create_list( SQL_OP, l ); }
 |  LOCALTIME opt_brackets
 			{ dlist *l = L();
			  append_list(l,
			  	append_string(L(), sa_strdup(SA, "localtime")));
	  		  $$ = _symbol_create_list( SQL_OP, l ); }
 |  LOCALTIMESTAMP opt_brackets
 			{ dlist *l = L();
			  append_list(l,
			  	append_string(L(), sa_strdup(SA, "localtimestamp")));
	  		  $$ = _symbol_create_list( SQL_OP, l ); }
 ;

opt_brackets:
   /* empty */	{ $$ = 0; }
 | '(' ')'	{ $$ = 1; }
 ;

string_funcs:
    SUBSTRING '(' scalar_exp FROM scalar_exp FOR scalar_exp ')'
			{ dlist *l = L();
			  dlist *ops = L();
  		  	  append_list(l,
				append_string(L(), sa_strdup(SA, "substring")));
  		  	  append_symbol(ops, $3);
  		  	  append_symbol(ops, $5);
  		  	  append_symbol(ops, $7);
			  append_list(l, ops);
		  	  $$ = _symbol_create_list( SQL_NOP, l ); }
  | SUBSTRING '(' scalar_exp ',' scalar_exp ',' scalar_exp ')'
			{ dlist *l = L();
			  dlist *ops = L();
  		  	  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, "substring")));
  		  	  append_symbol(ops, $3);
  		  	  append_symbol(ops, $5);
  		  	  append_symbol(ops, $7);
			  append_list(l, ops);
		  	  $$ = _symbol_create_list( SQL_NOP, l ); }
  | SUBSTRING '(' scalar_exp FROM scalar_exp ')'
			{ dlist *l = L();
  		  	  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, "substring")));
  		  	  append_symbol(l, $3);
  		  	  append_symbol(l, $5);
		  	  $$ = _symbol_create_list( SQL_BINOP, l ); }
  | SUBSTRING '(' scalar_exp ',' scalar_exp ')'
			{ dlist *l = L();
  		  	  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, "substring")));
  		  	  append_symbol(l, $3);
  		  	  append_symbol(l, $5);
		  	  $$ = _symbol_create_list( SQL_BINOP, l ); }
  | POSITION '(' scalar_exp sqlIN scalar_exp ')'
			{ dlist *l = L();
  		  	  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, "locate")));
  		  	  append_symbol(l, $3);
  		  	  append_symbol(l, $5);
		  	  $$ = _symbol_create_list( SQL_BINOP, l ); }
  | scalar_exp CONCATSTRING scalar_exp
			{ dlist *l = L();
  		  	  append_list(l,
  		  	  	append_string(L(), sa_strdup(SA, "concat")));
  		  	  append_symbol(l, $1);
  		  	  append_symbol(l, $3);
		  	  $$ = _symbol_create_list( SQL_BINOP, l ); }
 ;

column_exp_commalist:
    column_exp 		{ $$ = append_symbol(L(), $1 ); }
 |  column_exp_commalist ',' column_exp
			{ $$ = append_symbol( $1, $3 ); }
 ;

column_exp:
    ident '.' '*'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_string(l, NULL);
  		  $$ = _symbol_create_list( SQL_TABLE, l ); }
 |  scalar_exp opt_alias_name
		{ dlist *l = L();
  		  append_symbol(l, $1);
  		  append_string(l, $2);
  		  $$ = _symbol_create_list( SQL_COLUMN, l ); }
 |  null opt_alias_name
		{ dlist *l = L();
  		  append_symbol(l, $1 );
  		  append_string(l, $2);
  		  $$ = _symbol_create_list( SQL_COLUMN, l ); }
 |  search_condition opt_alias_name
		{ dlist *l = L();
  		  append_symbol(l, $1);
  		  append_string(l, $2);
  		  $$ = _symbol_create_list( SQL_COLUMN, l ); }
 ;

opt_alias_name:
    /* empty */	{ $$ = NULL; }
 |  AS ident	{ $$ = $2; }
 ;

atom:
    literal
	{ 
	  mvc *m = (mvc*)parm;
	
	  if (CACHE_MODE(m->mode) && 
	      (!m->argfixed || m->argc < MAXPARAMS-2) && m->cache){
	  	/* replace by argument */
	  	AtomNode *an = (AtomNode*)$1;
	
	  	sql_add_arg( m, an->a);
		an->a = NULL;
	  	/* we miss use SQL_COLUMN also for param's, maybe
	     		change SQL_COLUMN to SQL_IDENT */
 	  	$$ = _symbol_create_list( SQL_COLUMN,
			append_int(L(), m->argc-1));
	   } else {
	  	AtomNode *an = (AtomNode*)$1;
		atom *a = an->a; 
		an->a = atom_copy(a, SA); 
		atom_destroy(a);
		$$ = $1;
	   }
	}
 ;


/* change to set function */
aggr_ref:
    AGGR '(' '*' ')'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_int(l, FALSE);
  		  append_symbol(l, NULL);
		  $$ = _symbol_create_list( SQL_AGGR, l ); }
 |  AGGR '(' ident '.' '*' ')'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_int(l, FALSE);
  		  append_symbol(l, NULL);
		  $$ = _symbol_create_list( SQL_AGGR, l ); }
 |  AGGR '(' DISTINCT column_ref ')'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_int(l, TRUE);
  		  append_symbol(l, _symbol_create_list(SQL_COLUMN, $4));
		  $$ = _symbol_create_list( SQL_AGGR, l ); }
 |  AGGR '(' ALL scalar_exp ')'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_int(l, FALSE);
  		  append_symbol(l, $4);
		  $$ = _symbol_create_list( SQL_AGGR, l ); }
 |  AGGR '(' scalar_exp ')'
		{ dlist *l = L();
  		  append_string(l, $1);
  		  append_int(l, FALSE);
  		  append_symbol(l, $3);
		  $$ = _symbol_create_list( SQL_AGGR, l ); }
 ;

opt_sign:
   '+'		{ $$ = 1; }
 | '-' 		{ $$ = -1; }
 | /* empty */	{ $$ = 1; }
 ;

tz:
	WITH TIME ZONE	{ $$ = 1; }
 | /* empty */		{ $$ = 0; }
 ;

/* note: the maximum precision for both time and timestamp should be equal
 *       and at minimum 6.  The SQL standard prescribes that at least two
 *       fractional precisions are supported: 0 and 6, where 0 is the
 *       default for time, and 6 the default precision for timestamp.
 *       It might be nice to check for a certain maximum of precision in
 *       the future here.
 */
time_precision:
	'(' intval ')' 	{ $$ = $2+1; }
/* a time defaults to a fractional precision of 0 */
 | /* empty */		{ $$ = 0+1; }
 ;

timestamp_precision:
	'(' intval ')' 	{ $$ = $2+1; }
/* a timestamp defaults to a fractional precision of 6 */
 | /* empty */		{ $$ = 6+1; }
 ;

datetime_type:
    sqlDATE		{ sql_find_subtype(&$$, "date", 0, 0); }
 |  TIME time_precision tz 	
			{ if ($3)
				sql_find_subtype(&$$, "timetz", $2, 0); 
			  else
				sql_find_subtype(&$$, "time", $2, 0); 
			}
 |  TIMESTAMP timestamp_precision tz 
			{ if ($3)
				sql_find_subtype(&$$, "timestamptz", $2, 0); 
			  else
				sql_find_subtype(&$$, "timestamp", $2, 0); 
			}
 ;

non_second_datetime_field:
    YEAR		{ $$ = iyear; }
 |  MONTH		{ $$ = imonth; }
 |  DAY			{ $$ = iday; }
 |  HOUR		{ $$ = ihour; }
 |  MINUTE		{ $$ = imin; }
 ;

datetime_field:
    non_second_datetime_field
 |  SECOND		{ $$ = isec; }
 ;

start_field:
    non_second_datetime_field time_precision
		{ $$ = append_int(
			 	append_int( L(), $1), $2-1);  }
 ;

end_field:
    non_second_datetime_field
		{ $$ = append_int(
			 	append_int( L(), $1), 0);  }
 |  SECOND time_precision
		{ $$ = append_int(
			 	append_int( L(), isec), $2-1);  }
 ;

single_datetime_field:
    non_second_datetime_field time_precision
		{ $$ = append_int(
			 	append_int( L(), $1), $2-1);  }
 |  SECOND time_precision
		{ $$ = append_int(
			 	append_int( L(), isec), $2-1);  }
 ;

interval_qualifier:
    start_field TO end_field
	{ $$ =  append_list(
			append_list( L(), $1), $3 ); }
 |  single_datetime_field
	{ $$ =  append_list( L(), $1); }
 ;

interval_type:
    INTERVAL interval_qualifier	{
		int sk, ek;
		mvc *m = (mvc*)parm;
	  	int tpe;

		$$.type = NULL;
	  	if ( (tpe = parse_interval_qualifier( m, $2, &sk, &ek )) < 0){
			yyerror("incorrect interval");
			YYABORT;
	  	} else {
			if (tpe == 0){
				sql_find_subtype(&$$, "month_interval", ek, sk);
			} else {
				sql_find_subtype(&$$, "sec_interval", ek, sk);
			}
	  	}
	}
 ;

user:
    USER 
 |  SESSION_USER
 |  CURRENT_USER 
 ;

literal:
    string 	{ char *s = sql2str($1);
		  int len = _strlen(s);
		  sql_subtype t;
		  sql_find_subtype(&t, "char", len, 0 );
		  $$ = _newAtomNode( _atom_string(&t, s)); }

 |  HEXADECIMAL { int len = _strlen($1), i = 2, err = 0;
		  char buf[BUFSIZ];
		  char * hexa = $1;
	 	  sql_subtype t;
		  long res = 0;

		  while (i<len)
		  {
			res <<= 5;
			if ('0'<= hexa[i] && hexa[i] <= '9')
				res = res + (hexa[i] - '0');
			else if ('A' <= hexa[i] && hexa[i] <= 'F')
				res = res + (hexa[i] - 'A' + 10);
			else if ('a' <= hexa[i] && hexa[i] <= 'f')
				res = res + (hexa[i] - 'a' + 10);
			else
				err = 1; 
		  	i++;
		  }
		
		  if ( i >= 2 && i <= 6)
		  	sql_find_subtype(&t, "smallint", 16, 0);
		  else if ( i > 6 && i <= 10)
		  	sql_find_subtype(&t, "int", 32, 0);
		  else if ( i > 10 && i <= 18)
		  	sql_find_subtype(&t, "bigint", 64, 0);
		  else
			err = 1;
		  
		  if (err) {
			snprintf(buf, BUFSIZ, "invalid hexadecimal number or hexadecimal too large (%s)", $1);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		  } else {
			$$ = _newAtomNode( atom_int(&t, res));
		  }
		}
 |  sqlINT
		{ int digits = _strlen($1), err = 0, len = sizeof(lng);
		  lng value, *p = &value;
		  sql_subtype t;

		  lngFromStr($1, &len, &p);
		  if (value == lng_nil)
		  	err = 2;

		  /* find the most suitable data type for the given number */
		  if (!err) {
		    int bits = digits2bits(digits);
		    if (value >= GDK_sht_min && value <= GDK_sht_max)
		  	  sql_find_subtype(&t, "smallint", bits, 0 );
		    else if (value >= GDK_int_min && value <= GDK_int_max)
		  	  sql_find_subtype(&t, "int", bits, 0 );
		    else if ((value >= GDK_lng_min && value <= GDK_lng_max))
		  	  sql_find_subtype(&t, "bigint", bits, 0 );
		    else
			  err = 1;
		  }

		  if (err) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "integer value too large or not a number (%s)", $1);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		  } else {
		  	$$ = _newAtomNode( atom_int(&t, value));
		  }
		}
 |  INTNUM
		{ char *s = strip_extra_zeros(sa_strdup(SA, $1));
		  char *dot = strchr(s, '.');
		  int digits = _strlen(s) - 1;
		  int scale = digits - (int) (dot-s);
		  sql_subtype t;

		  if (digits <= 18) {
		  	double val = strtod($1,NULL);
		  	lng value = decimal_from_str(s);

		  	if (*s == '+' || *s == '-')
				digits --;
		  	sql_find_subtype(&t, "decimal", digits, scale );
		  	$$ = _newAtomNode( atom_dec(&t, value, val)); 
		   } else { 
		  	double val = strtod($1,NULL);

		  	sql_find_subtype(&t, "double", 51, 0 );
		  	$$ = _newAtomNode(atom_float(&t, val)); 
		   }
		}
 |  APPROXNUM
		{ sql_subtype t;
		  double val = strtod($1,NULL);

		  sql_find_subtype(&t, "double", 51, 0 );
		  $$ = _newAtomNode(atom_float(&t, val)); }
 |  sqlDATE string
		{ sql_subtype t;
		  atom *a;
		  int r;

 		  r = sql_find_subtype(&t, "date", 0, 0 );
		  if (!r || (a = atom_general(&t, $2, 0)) == NULL) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect date value (%s)", $2);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		  } else {
		  	$$ = _newAtomNode(a);
		} }
 |  TIME time_precision tz string
		{ sql_subtype t;
		  atom *a;
		  int r;

	          r = sql_find_subtype(&t, ($3)?"timetz":"time", $2, 0);
		  if (!r || (a = atom_general(&t, $4, 0)) == NULL) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect time value (%s)", $4);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		  } else {
		  	$$ = _newAtomNode(a);
		} }
 |  TIMESTAMP timestamp_precision tz string
		{ sql_subtype t;
		  atom *a;
		  int r;

 		  r = sql_find_subtype(&t, ($3)?"timestamptz":"timestamp",$2,0);
		  if (!r || (a = atom_general(&t, $4, 0)) == NULL) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect timestamp value (%s)", $4);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		  } else {
		  	$$ = _newAtomNode(a);
		} }
 |  interval_expression
 |  blob string
		{ sql_subtype t;
		  atom *a= 0;
		  int r;

		  $$ = NULL;
 		  r = sql_find_subtype(&t, "blob", 0, 0);
	          if (r && (a = atom_general(&t, $2, 0)) != NULL)
			$$ = _newAtomNode(a);
		  if (!$$) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect blob %s", $2);
			yyerror(buf);
			if (a)
				atom_destroy(a);
			YYABORT;
		  }
		}
 |  aTYPE string
		{ sql_subtype t;
		  atom *a= 0;
		  int r;

		  $$ = NULL;
		  r = sql_find_subtype(&t, $1, 0, 0);
	          if (r && (a = atom_general(&t, $2, 0)) != NULL)
			$$ = _newAtomNode(a);
		  if (!$$) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect %s %s", $1, $2);
			yyerror(buf);
			if (a)
				atom_destroy(a);
			YYABORT;
		  }
		}
 | type_alias string
		{ sql_subtype t; 
		  atom *a = 0;
		  int r;

		  $$ = NULL;
		  r = sql_find_subtype(&t, $1, 0, 0);
	          if (r && (a = atom_general(&t, $2, 0)) != NULL)
			$$ = _newAtomNode(a);
		  if (!$$) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "incorrect %s %s", $1, $2);
			yyerror(buf);
			if (a)
				atom_destroy(a);
			YYABORT;
		  }
		}
 | IDENT string
		{ mvc *m = (mvc*)parm;
		  sql_type *t = mvc_bind_type(m, $1);
		  atom *a;

		  $$ = NULL;
		  if (t) {
		  	sql_subtype tpe;
			sql_init_subtype(&tpe, t, 0, 0);
			a = atom_general(&tpe, $2, 0);
			if (a)
				$$ = _newAtomNode(a);
		  }
		  if (!t || !$$) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "type (%s) unknown", $1);
			yyerror(buf);
			YYABORT;
		  }
		}
 |  BOOL_FALSE
		{ sql_subtype t;
		  sql_find_subtype(&t, "boolean", 0, 0 );
		  $$ = _newAtomNode( atom_bool(&t, FALSE)); }
 |  BOOL_TRUE
		{ sql_subtype t;
		  sql_find_subtype(&t, "boolean", 0, 0 );
		  $$ = _newAtomNode( atom_bool(&t, TRUE)); }
 ;

interval_expression:
   INTERVAL opt_sign string interval_qualifier { 
		sql_subtype t;
		int sk, ek, tpe;
		mvc *m = (mvc*)parm;
	  	lng i = 0;
		int r = 0;

		$$ = NULL;
	  	if ( (tpe = parse_interval_qualifier( m, $4, &sk, &ek )) < 0){
			yyerror("incorrect interval");
			YYABORT;
	  	} else {
			if (tpe == 0){
				r=sql_find_subtype(&t, "month_interval", ek, sk);
			} else {
				r=sql_find_subtype(&t, "sec_interval", ek, sk);
			}
	  	}
	  	if (!r || (tpe = parse_interval( m, $2, $3, sk, ek, &i)) < 0 ){
			yyerror("incorrect interval");
			$$ = NULL;
			YYABORT;
	  	} else {
	  		$$ = _newAtomNode( atom_int(&t, i));
	  	}
	}

	/* miscellaneous */
 ;

qname:
    ident			{ $$ = append_string(L(), $1); }
 |  ident '.' ident		{ $$ = append_string(
					append_string(L(), $1), $3);}
 |  ident '.' ident '.' ident	{ $$ = append_string(
						append_string(
							append_string(L(), $1), 
							$3), 
						$5)
				;}
 ;

column_ref:
    ident		{ $$ = append_string(
				L(), $1); }

 |  ident '.' ident	{ $$ = append_string(
				append_string(
				 L(), $1), $3);}

 |  ident '.' ident '.' ident
    			{ $$ = append_string(
				append_string(
				 append_string(
				  L(), $1), $3), $5);}
 ;

cast_exp:
     CAST '(' cast_value AS data_type ')'
 	{ dlist *l = L();
	  append_symbol(l, $3);
	  append_type(l, &$5);
	  $$ = _symbol_create_list( SQL_CAST, l ); }
 |
     CONVERT '(' cast_value ',' data_type ')'
 	{ dlist *l = L();
	  append_symbol(l, $3);
	  append_type(l, &$5);
	  $$ = _symbol_create_list( SQL_CAST, l ); }
 ;

cast_value:
	scalar_exp
 | 	search_condition
 | 	null	 
 ;

case_exp:
     NULLIF '(' scalar_exp ',' scalar_exp ')'
		{ $$ = _symbol_create_list(SQL_NULLIF,
		   append_symbol(
		    append_symbol(
		     L(), $3), $5)); }
 |   COALESCE '(' scalar_exp ',' scalar_exp ')'
		{ $$ = _symbol_create_list(SQL_COALESCE,
		   append_symbol(
		    append_symbol(
		     L(), $3), $5)); }
 |   COALESCE '(' scalar_exp_list2 ')'
		{ $$ = _symbol_create_list(SQL_COALESCE, $3); }
 |   CASE scalar_exp when_value_list case_opt_else END
		{ $$ = _symbol_create_list(SQL_CASE,
		   append_symbol(
		    append_list(
		     append_symbol(
		      L(),$2),$3),$4)); }
 |   CASE when_search_list case_opt_else END
		 { $$ = _symbol_create_list(SQL_CASE,
		   append_symbol(
		    append_list(
		     L(),$2),$3)); }
 ;

scalar_exp_list2:
    scalar_exp ',' scalar_exp ',' scalar_exp
			{ $$ = append_symbol( L(), $1);
			  $$ = append_symbol( $$, $3);
			  $$ = append_symbol( $$, $5);
			}
 |  scalar_exp_list2 ',' scalar_exp
			{ $$ = append_symbol( $1, $3); }
 ;

when_value:
    WHEN scalar_exp THEN scalar_exp
			{ $$ = _symbol_create_list( SQL_WHEN,
			   append_symbol(
			    append_symbol(
			     L(), $2),$4)); }
 ;

when_value_list:
    when_value
			{ $$ = append_symbol( L(), $1);}
 |  when_value_list when_value
			{ $$ = append_symbol( $1, $2); }
 ;

when_search:
    WHEN search_condition THEN scalar_exp
			{ $$ = _symbol_create_list( SQL_WHEN,
			   append_symbol(
			    append_symbol(
			     L(), $2),$4)); }
 ;

when_search_list:
    when_search
			{ $$ = append_symbol( L(), $1); }
 |  when_search_list when_search
			{ $$ = append_symbol( $1, $2); }
 ;

case_opt_else:
    /* empty */	        { $$ = NULL; }
 |  ELSE scalar_exp	{ $$ = $2; }
 |  ELSE NULLX	 	{ $$ = _newAtomNode(NULL); }
 ;

		/* data types, more types to come */

nonzero:
	intval
		{ $$ = $1;
		  if ($$ <= 0) {
			$$ = -1;
			yyerror("Positive value greater than 0 expected");
			YYABORT;
		  }
		}
	;

posint:
	intval 	{ $$ = $1;
		  if ($$ < 0) {
			$$ = -1;
			yyerror("Positive value expected");
			YYABORT;
		  }
		}
	;

data_type:
    CHARACTER
			{ sql_find_subtype(&$$, "char", 1, 0); }
 |  varchar
			{ $$.type = NULL;
			  yyerror("CHARACTER VARYING needs a mandatory length specification");
			  YYABORT;
			}
 |  clob		{ sql_find_subtype(&$$, "clob", 0, 0); }
 |  CHARACTER '(' nonzero ')'
			{ sql_find_subtype(&$$, "char", $3, 0); }
 |  varchar '(' nonzero ')'
			{ sql_find_subtype(&$$, "varchar", $3, 0); }
 |  clob '(' nonzero ')'
			{ sql_find_subtype(&$$, "clob", $3, 0);
			  /* NOTE: CLOB may be called as CLOB(2K) which is equivalent
			   *       to CLOB(2048).  Due to 'nonzero' it is not possible
			   *       to enter this as the parser rejects it.  However it
			   *       might be a ToDo for the future.
			   *       See p. 125 SQL-99
			   */
			}
 |  blob		{ sql_find_subtype(&$$, "blob", 0, 0); }
 |  blob '(' nonzero ')'
			{ sql_find_subtype(&$$, "blob", $3, 0);
			  /* NOTE: BLOB may be called as BLOB(2K) which is equivalent
			   *       to BLOB(2048).  Due to 'nonzero' it is not possible
			   *       to enter this as the parser rejects it.  However it
			   *       might be a ToDo for the future.
			   *       See p. 85 SQL-99
			   */
			}
 |  SMALLINT		{ sql_find_subtype(&$$, "smallint", 0, 0); }
 |  sqlINTEGER		{ sql_find_subtype(&$$, "int", 0, 0); }
 |  BIGINT		{ sql_find_subtype(&$$, "bigint", 0, 0); }

 |  sqlDECIMAL		{ sql_find_subtype(&$$, "decimal", 1, 0); }
 |  sqlDECIMAL '(' nonzero ')'
			{ if ($3 > 18) {
				char buf[BUFSIZ];

				snprintf(buf, BUFSIZ, "decimal of %lld digits are not supported", $3);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else {
			        sql_find_subtype(&$$, "decimal", $3, 0); 
			  }
			}
 |  sqlDECIMAL '(' nonzero ',' posint ')'
			{ if ($5 >= $3 || $5 + $3 > 18) {
				char buf[BUFSIZ];

				if ($5 >= $3)
					snprintf(buf, BUFSIZ, "scale (%lld) should be less than precision (%lld)", $5, $3);
				else
					snprintf(buf, BUFSIZ, "decimal(%lld,%lld) isn't supported", $5, $3);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else {
				sql_find_subtype(&$$, "decimal", $3, $5);
			  }
			}
 |  sqlFLOAT		{ sql_find_subtype(&$$, "double", 0, 0); }
 |  sqlFLOAT '(' nonzero ')'
			{ if ($3 > 0 && $3 <= 24) {
				sql_find_subtype(&$$, "real", $3, 0);
			  } else if ($3 > 24 && $3 <= 53) {
				sql_find_subtype(&$$, "double", $3, 0);
			  } else {
				char buf[BUFSIZ];
				snprintf(buf, BUFSIZ, "number of digits for FLOAT values should be between 1 and 53");
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  }
			}
 |  sqlFLOAT '(' intval ',' intval ')'
			{ if ($5 >= $3) {
				char buf[BUFSIZ];

				snprintf(buf, BUFSIZ, "precision(%lld) should be less than number of digits(%lld)", $5, $3);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else if ($3 > 0 && $3 <= 24) {
				sql_find_subtype(&$$, "real", $3, $5);
			  } else if ($3 > 24 && $3 <= 53) {
				sql_find_subtype(&$$, "double", $3, $5);
			  } else {
				char buf[BUFSIZ];
				snprintf(buf, BUFSIZ, "number of digits for FLOAT values should be between 1 and 53");
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  }
			}
 | sqlDOUBLE 		{ sql_find_subtype(&$$, "double", 0, 0); }
 | sqlDOUBLE PRECISION	{ sql_find_subtype(&$$, "double", 0, 0); }
 | sqlREAL 		{ sql_find_subtype(&$$, "real", 0, 0); }
 | datetime_type
 | interval_type
 | aTYPE		{ sql_find_subtype(&$$, $1, 0, 0); }
 | aTYPE '(' nonzero ')'
			{ sql_find_subtype(&$$, $1, $3, 0); }
 | type_alias 		{ sql_find_subtype(&$$, $1, 0, 0); }
 | type_alias '(' nonzero ')'
			{ sql_find_subtype(&$$, $1, $3, 0); }
 | type_alias '(' intval ',' intval ')'
			{ if ($5 >= $3) {
				char buf[BUFSIZ];

				snprintf(buf, BUFSIZ, "precision(%lld) should be less than number of digits(%lld)", $5, $3);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else {
			 	sql_find_subtype(&$$, $1, $3, $5);
			  }
			}
 | IDENT		{ mvc *m = (mvc*)parm;
			  sql_type *t = mvc_bind_type(m, $1);
			  if (!t) {
				char buf[BUFSIZ];

				snprintf(buf, BUFSIZ, "type (%s) unknown", $1);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else {
				sql_init_subtype(&$$, t, 0, 0);
			  }
			}

 | IDENT '(' nonzero ')'
			{ mvc *m = (mvc*)parm;
			  sql_type *t = mvc_bind_type(m, $1);
			  if (!t) {
				char buf[BUFSIZ];

				snprintf(buf, BUFSIZ, "type (%s) unknown", $1);
				yyerror(buf);
				$$.type = NULL;
				YYABORT;
			  } else {
				sql_init_subtype(&$$, t, $3, 0);
			  }
			}

 ;

type_alias:
 ALIAS
	{ 	char *t = sql_bind_alias($1);
	  	if (!t) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "type (%s) unknown", $1);
			yyerror(buf);
			$$ = NULL;
			YYABORT;
		}
		$$ = t;
	}
 ;

varchar:
	VARCHAR				{ $$ = $1; }
 |	CHARACTER VARYING		{ $$ = $1; }
;

clob:
	CLOB				{ $$ = $1; }
 |	CHARACTER LARGE OBJECT		{ $$ = $1; }
;
blob:
	sqlBLOB				{ $$ = $1; }
 |	BINARY LARGE OBJECT		{ $$ = $1; }
;

column:			ident ;

authid: 		restricted_ident ;

restricted_ident:
    IDENT	{ $$ = $1; }
 |  aTYPE	{ $$ = $1; }
 |  ALIAS	{ $$ = $1; }
 |  AGGR	{ $$ = $1; } 	/* without '(' */
 |  RANK	{ $$ = $1; }	/* without '(' */
 ;

ident:
    IDENT	{ $$ = $1; }
 |  aTYPE	{ $$ = $1; }
 |  ALIAS	{ $$ = $1; }
 |  AGGR	{ $$ = $1; } 	/* without '(' */
 |  RANK	{ $$ = $1; }	/* without '(' */
 |  non_reserved_word
 ;

non_reserved_word:  /* (lexicographically sorted for convenience) */
  LARGE		{ $$ = sa_strdup(SA, "large"); }	/* sloppy: officially reserved */
| sqlNAME	{ $$ = sa_strdup(SA, "name"); }
| OBJECT	{ $$ = sa_strdup(SA, "object"); }	/* sloppy: officially reserved */
| PASSWORD	{ $$ = sa_strdup(SA, "password"); }	/* neither reserved nor non-reserv. */
| PATH		{ $$ = sa_strdup(SA, "path"); }		/* sloppy: officially reserved */
| PRECISION 	{ $$ = sa_strdup(SA, "precision"); }	/* sloppy: officially reserved */
| PRIVILEGES	{ $$ = sa_strdup(SA, "privileges"); }	/* sloppy: officially reserved */
| ROLE		{ $$ = sa_strdup(SA, "role"); }	 	/* neither reserved nor non-reserv. */
| sqlSIZE	{ $$ = sa_strdup(SA, "size"); }		/* sloppy: officially reserved */
| TYPE		{ $$ = sa_strdup(SA, "type"); }
| RELEASE	{ $$ = sa_strdup(SA, "release"); }	/* sloppy: officially reserved */
| VALUE		{ $$ = sa_strdup(SA, "value"); }	/* sloppy: officially reserved */
| ZONE		{ $$ = sa_strdup(SA, "zone"); }		/* sloppy: officially reserved */

|  CACHE	{ $$ = sa_strdup(SA, "cache"); }
|  DATA 	{ $$ = sa_strdup(SA, "data"); }
|  DIAGNOSTICS 	{ $$ = sa_strdup(SA, "diagnostics"); }
|  MATCH	{ $$ = sa_strdup(SA, "match"); }
|  OPTIONS	{ $$ = sa_strdup(SA, "options"); }
|  ROW		{ $$ = sa_strdup(SA, "row"); }
|  KEY		{ $$ = sa_strdup(SA, "key"); }
|  LANGUAGE	{ $$ = sa_strdup(SA, "language"); }
|  LEVEL	{ $$ = sa_strdup(SA, "level"); }
|  sqlSESSION	{ $$ = sa_strdup(SA, "session"); }
|  sqlDATE	{ $$ = sa_strdup(SA, "date"); }
|  TIME 	{ $$ = sa_strdup(SA, "time"); }
|  TIMESTAMP	{ $$ = sa_strdup(SA, "timestamp"); }
|  INTERVAL	{ $$ = sa_strdup(SA, "interval"); }

|  PREPARE	{ $$ = sa_strdup(SA, "prepare"); }
|  EXECUTE	{ $$ = sa_strdup(SA, "execute"); }
|  SQL_EXPLAIN	{ $$ = sa_strdup(SA, "explain"); }
|  SQL_PROFILE	{ $$ = sa_strdup(SA, "profile"); }
|  SQL_DEBUG	{ $$ = sa_strdup(SA, "debug"); }
|  SQL_TRACE	{ $$ = sa_strdup(SA, "trace"); }
|  AUTO_COMMIT	{ $$ = sa_strdup(SA, "auto_commit"); }

;

name_commalist:
    ident	{ $$ = append_string(L(), $1); }
 |  name_commalist ',' ident
			{ $$ = append_string($1, $3); }
 ;

intval:
	sqlINT	{ $$ = strtol($1,NULL,10); }
 |	IDENT	{ mvc *m = (mvc*)parm;
		  char *name = $1;
		  sql_subtype *tpe;
		  stmt *var;

		  if (!(var = stack_find_var(m, name))) {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "constant (%s) unknown", $1);
			yyerror(buf);
			$$ = 0;
			YYABORT;
		  }
		  tpe = tail_type(var);
		  if (tpe->type->localtype == TYPE_lng ||
		      tpe->type->localtype == TYPE_int ||
		      tpe->type->localtype == TYPE_sht ) {
			$$ = stack_get_number(m, name);
		  } else {
			char buf[BUFSIZ];

			snprintf(buf, BUFSIZ, "constant (%s) has wrong type (number expected)", $1);
			yyerror(buf);
			$$ = 0;
			YYABORT;
		  }
		}
 ;

string:
    STRING
		{ $$ = $1; }
 |  STRING string
		{ char *s = strconcat($1,$2); 
	 	  $$ = sa_strdup(SA, s);	
		  _DELETE(s);
		}
 ;

exec:
     EXECUTE exec_ref
		{ mvc *m = (mvc*)parm;
		  m->mode = m_execute;
		  $$ = $2; }
 ;

exec_ref:
    intval '(' ')'
	{ dlist *l = L();
  	  append_int(l, $1);
  	  append_list(l, NULL);
	  $$ = _symbol_create_list( SQL_NOP, l ); }
|   intval '(' atom_commalist ')'
	{ dlist *l = L();
  	  append_int(l, $1);
  	  append_list(l, $3);
	  $$ = _symbol_create_list( SQL_NOP, l ); }
 ;

/* path specification> 

Specify an order for searching for an SQL-invoked routine.

CURRENTLY only parsed 
*/

opt_path_specification: 
	/* empty */ 	{ $$ = NULL; }
   |	path_specification
   ;

path_specification: 
        PATH schema_name_list 	{ $$ = _symbol_create_list( SQL_PATH, $2); }
   ;

schema_name_list: name_commalist ;

%%
char *token2string(int token)
{
	switch (token) {
#define SQL(TYPE) case SQL_##TYPE : return #TYPE
	SQL(CREATE_SCHEMA);
	SQL(CREATE_TABLE);
	SQL(CREATE_VIEW);
	SQL(CREATE_INDEX);
	SQL(CREATE_ROLE);
	SQL(CREATE_USER);
	SQL(CREATE_TYPE);
	SQL(CREATE_FUNC);
	SQL(CREATE_SEQ);
	SQL(CREATE_TRIGGER);
	SQL(DROP_SCHEMA);
	SQL(DROP_TABLE);
	SQL(DROP_VIEW);
	SQL(DROP_INDEX);
	SQL(DROP_ROLE);
	SQL(DROP_USER);
	SQL(DROP_TYPE);
	SQL(DROP_FUNC);
	SQL(DROP_SEQ);
	SQL(DROP_TRIGGER);
	SQL(ALTER_TABLE);
	SQL(ALTER_SEQ);
	SQL(ALTER_USER);
	SQL(DROP_COLUMN);
	SQL(DROP_CONSTRAINT);
	SQL(DROP_DEFAULT);
	SQL(DECLARE);
	SQL(SET);
	SQL(PREP);
	SQL(NAME);
	SQL(USER);
	SQL(PATH);
	SQL(CHARSET);
	SQL(SCHEMA);
	SQL(TABLE);
	SQL(TYPE);
	SQL(CASE);
	SQL(CAST);
	SQL(RETURN);
	SQL(IF);
	SQL(ELSE);
	SQL(WHILE);
	SQL(COLUMN);
	SQL(COLUMN_OPTIONS);
	SQL(COALESCE);
	SQL(CONSTRAINT);
	SQL(CHECK);
	SQL(DEFAULT);
	SQL(NOT_NULL);
	SQL(NULL);
	SQL(NULLIF);
	SQL(UNIQUE);
	SQL(PRIMARY_KEY);
	SQL(FOREIGN_KEY);
	SQL(BEGIN);
#define TR(TYPE) case TR_##TYPE : return #TYPE
	TR(COMMIT);
	TR(ROLLBACK);
	TR(SAVEPOINT);
	TR(RELEASE);
	TR(START);
	TR(MODE);
	SQL(INSERT);
	SQL(DELETE);
	SQL(UPDATE);
	SQL(CROSS);
	SQL(JOIN);
	SQL(SELECT);
	SQL(CONNECT);
	SQL(DISCONNECT);
	SQL(DATABASE);
	SQL(PORT);
	SQL(WHERE);
	SQL(FROM);
	SQL(UNIONJOIN);
	SQL(UNION);
	SQL(EXCEPT);
	SQL(INTERSECT);
	SQL(VALUES);
	SQL(ASSIGN);
	SQL(ORDERBY);
	SQL(GROUPBY);
	SQL(DESC);
	SQL(AND);
	SQL(OR);
	SQL(NOT);
	SQL(EXISTS);
	SQL(NOT_EXISTS);
	SQL(OP);
	SQL(UNOP);
	SQL(BINOP);
	SQL(NOP);
	SQL(BETWEEN);
	SQL(NOT_BETWEEN);
	SQL(LIKE);
	SQL(NOT_LIKE);
	SQL(IN);
	SQL(NOT_IN);
	SQL(GRANT);
	SQL(GRANT_ROLES);
	SQL(REVOKE);
	SQL(REVOKE_ROLES);
	SQL(EXECUTE);
	SQL(PRIVILEGES);
	SQL(ROLE);
	SQL(PARAMETER);
	SQL(FUNC);
	SQL(AGGR);
	SQL(RANK);
	SQL(COMPARE);
	SQL(TEMP_LOCAL);
	SQL(TEMP_GLOBAL);
	SQL(INT_VALUE);
	SQL(ATOM);
	SQL(USING);
	SQL(WHEN);
	SQL(ESCAPE);
	SQL(COPYFROM);
	SQL(COPYTO);
	SQL(EXPORT);
	SQL(NEXT);
	SQL(MULSTMT);
	SQL(WITH);
	}
	return "unknown";	/* just needed for broken compilers ! */
}

void *sql_error( mvc * sql, int error_code, char *format, ... )
{
	va_list	ap;

	va_start (ap,format);
	if (sql->errstr[0] == '\0') 
		vsnprintf(sql->errstr, ERRSIZE-1, _(format), ap);
	if (!sql->session->status)
		sql->session->status = -error_code;
	va_end (ap);
	return NULL;
}

int parse_error(mvc * c, char *err)
{
	(void)sql_error( c, 4,
		 "!%s in: \"%s\"\n",
		 err, QUERY(c->scanner));
	return 1;
}
