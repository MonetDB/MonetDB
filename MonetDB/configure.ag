dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/utils/Mx/Mx.h)
AC_CONFIG_AUX_DIR(conf)

dnl ----------------------
AM_INIT_AUTOMAKE("Monet", 4.3.3)
AM_CONFIG_HEADER(config.h:conf/config.h.in)
AC_CANONICAL_HOST

HOST=[$host]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])
PREFIX=[$prefix]
AC_DEFINE_UNQUOTED(MONET_PREFIX, "$PREFIX", [directory prefix])

MONET_PREFIX=[`(mkdir -p $PREFIX && cd $PREFIX && pwd)`]
AC_SUBST(MONET_PREFIX)
MONET_BUILD=[`pwd`]
AC_SUBST(MONET_BUILD)
MONET_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(MONET_SOURCE)

dnl we use LEXLIB=-ll because this is usually correctly installed 
dnl and -lfl usually only in the 32bit version
dnl some dirty hacks
MEL_LIBS=""
thread_safe_flag_spec="-D_REENTRANT"
[ case "$host_os" in
  solaris*)
    case "$CXX" in
      CC*) 
	echo "$CXX=CC"
        MEL_LIBS="-z muldefs"
        thread_safe_flag_spec="-mt" ;;
      *) ;;
    esac
    LEXLIB=-ll
    ;;
  irix*)
    case "$CC" in
      *-32|*"-32 ") libsuff="";;
      "cc -32"|*-n32|*"-n32 ") libsuff=n32;;
      "cc -64"|*-64|*"-64 ") libsuff=64;;
      *) libsuff=n32;;
    esac
    LEXLIB=-ll
    ;;
  aix*)
    thread_safe_flag_spec="-D_THREAD_SAFE $thread_safe_flag_spec"
    ;;
esac ]
AC_SUBST(MEL_LIBS)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_INSTALL
AM_PROG_LEX
AC_PROG_YACC
AC_DISABLE_STATIC
AC_ENABLE_SHARED
AM_PROG_LIBTOOL
dnl AC_PROG_CC_STDC

AC_PROG_LN_S
AC_CHECK_PROG(RM,rm,rm -f)
AC_CHECK_PROG(MV,mv,mv -f)
AC_CHECK_PROG(LATEX,latex,latex)
AC_CHECK_PROG(PDFLATEX,pdflatex,pdflatex)
AC_CHECK_PROG(DVIPS,dvips,dvips)
AC_CHECK_PROG(FIG2DEV,fig2dev,fig2dev)
AM_CONDITIONAL(DOCTOOLS, test -n "$LATEX" -a -n "$FIG2DEV" -a -n "$DVIPS") 
AC_PATH_PROG(BASH,bash, /usr/bin/bash, $PATH:/bin:/usr/bin:/usr/local/bin)

dnl to shut up automake (.m files are used for mel not for objc)
AC_CHECK_TOOL(OBJC,objc)

#AM_DEPENDENCIES(CC)
#AM_DEPENDENCIES(CXX)

dnl AC_CHECK_PROG(CP,cp,cp -f)
dnl AC_CHECK_PROG(MKDIR,mkdir,mkdir -p)
dnl AC_CHECK_PROG(TAGS,ctags,ctags)

dnl --enable-debug
CFLAGS="$CFLAGS"
AC_ARG_ENABLE(debug,
[  --enable-debug          enable full debugging [default=off]],
  enable_debug=$enableval, enable_debug=no)
if test "x$enable_debug" = xyes; then
  if test "x$GCC" = xyes; then
    CFLAGS="$CFLAGS -O0"
  fi
  CFLAGS="$CFLAGS -g"
fi

dnl --enable-optimize
AC_ARG_ENABLE(optimize,
[  --enable-optimize       enable extra optimization [default=off]],
  enable_optim=$enableval, enable_optim=no)
if test "x$enable_optim" = xyes; then
  dnl Optimization flags
  if test "x$enable_debug" = xno; then
    if test "x$GCC" = xyes; then
      dnl -fomit-frame-pointer crashes memprof
      case "$host" in
      i*86-*-*)       CFLAGS="$CFLAGS -O6 -fomit-frame-pointer -finline-functions -malign-loops=4 -malign-jumps=4 -malign-functions=4 -ffast-math -fexpensive-optimizations -funroll-all-loops  -funroll-loops -frerun-cse-after-loop -frerun-loop-opt";;
      *-sun-solaris*) CFLAGS="$CFLAGS -O2 -fomit-frame-pointer -finline-functions";;
      *)              CFLAGS="$CFLAGS -O6 -fomit-frame-pointer -finline-functions";;
      esac     
    else
      case "$host" in
      *irix6.5*)      CFLAGS="$CFLAGS -O3 -Ofast=IP27 -OPT:alias=restrict -IPA"
                      LDFLAGS="$LDFLAGS -IPA"
                      ;;
      *-sun-solaris*) CFLAGS="$CFLAGS -xO5"
                      ;;
      esac     
    fi
	
  fi
fi

dnl --enable-warning (only gcc)
AC_ARG_ENABLE(warning,
[  --enable-warning           enable extended compiler warnings [default=off]],
  enable_warning=$enableval, enable_warning=no)
if test "x$enable_warning" = xyes; then
  if test "x$GCC" = xyes; then
    CFLAGS="$CFLAGS -ansi -pedantic"
  fi
fi

dnl systems checks
AC_AIX


dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h getopt.h malloc.h pwd.h dlfcn.h)
AC_CHECK_HEADERS(time.h sys/time.h sys/utime.h)
AC_CHECK_HEADERS(sys/file.h sys/param.h sys/times.h sys/mman.h sys/signal.h)
AC_CHECK_HEADERS(rlimit.h netdb.h sys/resource.h)

dnl libpthread
have_pthread=auto
PTHREAD_LIBS=""
PTHREAD_INCS=""
AC_ARG_WITH(pthread,
[  --with-pthread=DIR     pthread library is installed in DIR], 
	[have_pthread="$withval"])
if test "x$have_pthread" != xno; then
  if test "x$have_pthread" != xauto; then
    PTHREAD_LIBS="-L$withval/lib"
    PTHREAD_INCS="-I$withval/include"
  fi

  save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS $PTHREAD_INCS"
  AC_CHECK_HEADERS(pthread.h semaphore.h) 
  CFLAGS="$save_CFLAGS"

  save_LIBS="$LIBS"
  LIBS="$LIBS $PTHREAD_LIBS"
  AC_CHECK_LIB(pthread, sem_init, 
	[ PTHREAD_LIBS="$PTHREAD_LIBS -lpthread" 
          AC_DEFINE(HAVE_LIBPTHREAD) 
	  have_pthread=yes ] , 
	dnl sun
	[ AC_CHECK_LIB(pthread, sem_post, 
		[ PTHREAD_LIBS="$PTHREAD_LIBS -lpthread -lposix4" 
          	AC_DEFINE(HAVE_LIBPTHREAD) 
	  	have_pthread=yes ] , [ have_pthread=no], "-lposix4" )
	] )
  LIBS="$save_LIBS"

  if test "x$have_pthread" != xyes; then
    PTHREAD_LIBS=""
    PTHREAD_INCS=""
  fi
fi
AC_SUBST(PTHREAD_LIBS)
AC_SUBST(PTHREAD_INCS)

dnl libreadline
have_readline=auto
READLINE_LIBS=""
AC_ARG_WITH(readline,
[  --with-readline=DIR     readline library is installed in DIR], 
	[have_readline="$withval"])
if test "x$have_readline" != xno; then
  if test "x$have_readline" != xauto; then
    READLINE_LIBS="-L$withval/lib"
  fi

  save_LIBS="$LIBS"
  LIBS="$LIBS $READLINE_LIBS"
  AC_CHECK_LIB(readline$libsuff, readline, 
	[ READLINE_LIBS="$READLINE_LIBS -lreadline$libsuff -ltermcap" 
          AC_DEFINE(HAVE_LIBREADLINE) 
	  have_readline=yes ]
	, have_readline=no, "-ltermcap" )
  LIBS="$save_LIBS"

  if test "x$have_readline" != xyes; then
    READLINE_LIBS=""
  fi
fi
AC_SUBST(READLINE_LIBS)

DL_LIBS=""
AC_CHECK_LIB(dl, dlopen, [ DL_LIBS="-ldl" ] )
AC_SUBST(DL_LIBS)

MALLOC_LIBS=""
AC_CHECK_LIB(malloc, malloc, [ MALLOC_LIBS="-lmalloc" ] )
AC_SUBST(MALLOC_LIBS)

save_LIBS="$LIBS"
LIBS="$LIBS $MALLOC_LIBS"
AC_CHECK_FUNCS(mallopt mallinfo)
LIBS="$save_LIBS"


SOCKET_LIBS=""
AC_CHECK_LIB(socket, socket, [ SOCKET_LIBS="-lsocket -lnsl" ], [], "-lnsl" )
AC_SUBST(SOCKET_LIBS)

dnl check for z (de)compression library (default /usr and /usr/local)
have_z=auto
Z_CFLAGS=""
Z_LIBS=""
AC_ARG_WITH(z,
[  --with-z=DIR     z library is installed in DIR], have_z="$withval")
if test "x$have_z" != xno; then
  if test "x$have_z" != xauto; then
    Z_CFLAGS="-I$withval/include"
    Z_LIBS="-L$withval/lib"
  fi

  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS $Z_CFLAGS"
  AC_CHECK_HEADER(zlib.h, have_z=yes, have_z=no)
  CPPFLAGS="$save_CPPFLAGS"

  if test "x$have_z" = xyes; then
  	save_LIBS="$LIBS"
  	LIBS="$LIBS $Z_LIBS"
  	AC_CHECK_LIB(z, gzopen, Z_LIBS="$Z_LIBS -lz"
        	AC_DEFINE(HAVE_LIBZ) have_z=yes, have_z=no)
  	LIBS="$save_LIBS"
  fi

  if test "x$have_z" != xyes; then
    Z_CFLAGS=""
    Z_LIBS=""
  fi
fi
AC_SUBST(Z_CFLAGS)
AC_SUBST(Z_LIBS)

dnl check for bz2 (de)compression library (default /usr and /usr/local)
have_bz=auto
BZ_CFLAGS=""
BZ_LIBS=""
AC_ARG_WITH(bz2,
[  --with-bz2=DIR     bz2 library is installed in DIR], have_bz="$withval")
if test "x$have_bz" != xno; then
  if test "x$have_bz" != xauto; then
    BZ_CFLAGS="-I$withval/include"
    BZ_LIBS="-L$withval/lib"
  fi

  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS $BZ_CFLAGS"
  AC_CHECK_HEADER(bzlib.h, have_bz=yes, have_bz=no)
  CPPFLAGS="$save_CPPFLAGS"

  if test "x$have_bz" = xyes; then
  	save_LIBS="$LIBS"
  	LIBS="$LIBS $BZ_LIBS"
  	AC_CHECK_LIB(bz2, BZ2_bzopen, BZ_LIBS="$BZ_LIBS -lbz2"
        	AC_DEFINE(HAVE_LIBBZ2) have_bz=yes, have_bz=no)
  	LIBS="$save_LIBS"
  fi

  if test "x$have_bz" != xyes; then
    BZ_CFLAGS=""
    BZ_LIBS=""
  fi
fi
AC_SUBST(BZ_CFLAGS)
AC_SUBST(BZ_LIBS)



dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_STAT
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE
AC_LANG_CPLUSPLUS
AC_TRY_COMPILE( [], [ bool x = true; ], [ AC_DEFINE(HAVE_BOOL) ] )
AC_LANG_C
AC_C_BIGENDIAN
AC_TRY_COMPILE( [], [ long long x; ], [ AC_DEFINE(HAVE_LONGLONG)]  )
AC_CHECK_SIZEOF(char,1)
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)
AC_CHECK_SIZEOF(long long,8)

dnl only win32 AC_TRY_COMPILE( [], [ __int64 x; ], [ AC_DEFINE(HAVE__INT64)] )

dnl Checks for library functions.
AC_FUNC_ALLOCA 
AC_FUNC_MMAP
AC_FUNC_SETPGRP
AC_TYPE_SIGNAL
AC_FUNC_UTIME_NULL
AC_FUNC_VPRINTF
AC_FUNC_MEMCMP 
AC_CHECK_FUNCS(mkdir rmdir getcwd getopt getrlimit vsnprintf)
AC_CHECK_FUNCS(strcspn strdup strstr strtod strtol strerror )
AC_CHECK_FUNCS(gethostname gettimeofday setenv putenv select getpgid)

AC_CHECK_FUNCS(ctime_r)
ctime_r3=yes
AC_MSG_CHECKING(ctime_r3)
AC_TRY_COMPILE( [#include <time.h> ], 
[ char buf[26]; time_t t; ctime_r(&t,buf,26); ], 
[ AC_DEFINE(HAVE_CTIME_R3) ], [ ctime_r3=no ])
AC_MSG_RESULT($ctime_r3)

SHARED_LIBS=''
[
if [ "$enable_static" = "yes" ]; then
	CFLAGS="$CFLAGS -DSTATIC"
	SHARED_LIBS='$(SHARED_LIBS)'
fi
]

AC_SUBST(SHARED_LIBS)
AC_SUBST(thread_safe_flag_spec)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)

